# Concurrent_Programming_with_Async

Async を使った並行プログラミング

https://stackoverflow.com/questions/1897993/what-is-the-difference-between-concurrent-programming-and-parallel-programming/60698972#60698972

### Concurrent vs Parallel

Con - current
- 2つ以上の流れが、合体して1つになってるイメージ
- 1つのCPUコアでも、時間毎にスレッドを切り替えることでConcurrentプログラミングできる
- タスクは任意の順序で前後する

Parallel
- 2つ以上の流れが、(合体せず)並列に流れるイメージ
- 計算は独立し、別々のコアで計算され、結果だけ統合される
- タスクは同時

すべての Parallel は Concurrent だが、
Concurrent が Parallel とは限らない

Concurrent ∋ Parallel

> In programming, concurrency is the composition of independently executing processes, while parallelism is the simultaneous execution of (possibly related) computations.
> - Andrew Gerrand -


## 概要
外の世界と対話するロジックは、多くの場合待ち時間に支配される

- 入力待ち
  - マウスクリック待ち
  - キー入力待ち
- 読み込み待ち
  - ディスクからのデータ取得待ち
- 通信待ち
  - ネットワークバッファの空き容量を待つ
  
複数の異なるイベントを同時に待つ必要があり、最初に起こったものに即座に反応する。

## concurrent へのアプローチ

### Preemptive system thread
同時待機を必要とする可能性のあるタスクに、OSスレッドをアタへプログラム全体を停止せずに対応する。
つまり、専用スレッドを用意しておくこと

### Single Thread
タイムアウト、マウスクリックなどのイベントに対し
コールバック関数を登録しておき、イベントループを実行する。

Javascript などのシングルスレッドランタイムのプログラミング処理系や、
その他多くのGUIツールキットでよく見られる

## trade-offs

(Preemptive) Sysytem Thread
- スレッドあたりのリソースが大量に必要(メモリなど)
- スレッドの実行を任意で休止できるので、共有リソースを慎重に保護する必要がある
  - Lock(Mutex, Semapho)
  - 条件変数
    - エラーが非常に発生しやすい

Single Thread
- 複雑な同期を必要としない(単一タスクのみ実行)
- システムの制御構造が逆転
  - イベント駆動型プログラム
  - コールバック地獄


# Async
両方の世界のベストを提供するためのハイブリッドモデル
Promiseのように、成功/失敗時のコールバック関数を登録するモデルよりも
非同期処理が書きやすい構文

# Async Basis

基本的なFile I/O プログラム

```ml
open Core;;
Out_channel.write_all "test.txt" ~data:"This is only a test.";;
In_channel.read_all "test.txt";;
```

`read_all: string -> string = <fun>` のように、基本的にブロッキング操作
つまり、読み込みが完了するまで戻り値が返せない
そのため、外のプログラムの実行を止める

```ml
#require "async";;
open Async;;
Reader.file_contents;; (* : string -> string Deferred.t = <fun> *)
```
`Async.Reader.file_contents`関数は、`string Deferred.t`型を返す
読み込みが完了しなくても値を返すので、他のプログラムを止めない

deffer = 延期


## utop の場合

トップレベルで`#require "Async";;`をつかってAsyncをロードすると、
非同期処理を行ったときに`Deferred.t`の計算を自動でスケジューリングして、
使おうとしたときにDeferredの中身が決定するまでブロックする

```ml
contents;;
- : string = "This is only a test."
```


# Async.Deferred.peek
スタンドアロンプロジェクトで使う場合、
`Deferred.t`型の中身は、自分でスケジューラーを起動しないと取得できない。

```ml
open Core
open Async

let () =
  let contents = Async.Reader.file_contents "text.txt" in
  match Async.Deferred.peek contents with
  | Some str -> print_string str
  | None -> print_string "Nothing!"

```
必要な場面で、延期された`Deferred.t`型の中身を取り出すには
`Async.Deferred.peek`関数を使う

が、スケジューラーを起動してない状態だと、`None`すら返さないらしい。



# Deferred.bind
bind = 変数や引数に値を紐付けること

`bind`関数は、ある関数の引数に`Deferred.t`の中身をバインドする。
関数は`'a -> 'b Deferred.t` なので、モナド的なやつ。

```ml
let uppercase_file filename =
  Deferred.bind (Reader.file_contents filename) ~f:(fun text ->
      Writer.save filename ~contents:(String.uppercase_ascii text))
```

`>>=`演算子が用意されてるので、もう完璧にモナド
`Or_error`モジュールをincludeしてるっぽい

```ml
let uppercase_file filename =
  Reader.file_contents filename
  >>= fun text -> Writer.save filename ~contents:(String.uppercase_ascii text)
```

見やすくなった
インデントしないで`>>=`を書くのは`infix bind`演算子を使用する標準的な方法



## 入力ファイルの行数を数えて返す

ただし、bind関数のコールバック関数は、絶対失敗しない関数でも`Deferred.t`でラップして返すことを求められる。
`Deferred.return`関数を使う

```ml
let count_lines filename =
  Reader.file_contents filename
  >>= fun text -> return (List.length (String.split ~on:'\n' text))
```




# Deferred.map
実際には、`bind` と `return` を一緒に呼ぶのはかなり一般的なパターンであり、
それをまとめたショートカット関数が`map`

よく使うので、`>>|` という演算子が用意されている

```ml
Deferred.map;;
- : 'a Deferred.t -> f:('a -> 'b) -> 'b Deferred.t = <fun>
```

`'b Deferred.t`を返す関数ではなく、普通の値を返す関数を渡せば
それが自動でラップされて帰る。

```ml
let count_lines filename =
  Reader.file_contents filename
  >>| fun text -> List.length (String.split text ~on:'\n')
```

`>>|` 、`map`、覚えよう
(`map` = `bind ('a -> 'b Deferred.t) & return 'b`)



# Let_syntax

`#require "ppx_let";;`

`let%bind`を使うことで、Monadを扱う特殊なlet bind を使える
`ppx_let`が必要


## let%bind vs let%map

```ml
let count_lines filename =
  let%bind text = Reader.file_contents filename in
  return (List.length (String.split text ~on:'\n'))

let count_lines filename =
  let%map text = Reader.file_contents filename in
  List.length (String.split text ~on:'\n')
```

`let%map`というシンタックスも使えるようになる
この構文でバインドした変数を関数の中で使うと、モナドに関数をmapしたのとおなじになる。


### let syntax の意義
OCamlの組み込みlet式によるbindと、コーディング的な類似性を強調して
コードを均一に読みやすくする。
RWOではモナディックプログラミングになれたら、Let syntax を使ったほうが良いと行っている

`>>= : bind infix` `>>| : map infix`を使用して書くのと、内容的には変わらない
プロジェクトで採用された方に合わせたほうが良い。

ただし、let syntax は ppx を利用したシンタックスシュガーみたいなものなので、
infix表記のほうが関数型本来の書き方っぽいのかもしれない


infix = 中置子



# Ivar & Upon

Deferred値 は `bind, map, return` 組み合わせで構築される。
でも、プログラム的にいつDeferredが決定されるか決めたいときもある

そんなときに使うのが`ivar`
(Concurrent ML, 1990s に開発されたプログラミング言語で使われる用語)

ivar の `i` は、incrementalのi


# Ivar: create & read & fill

Ivarを扱うための基本的な3つの操作

`Ivar.`
- `create` : 空のIvar.tを作る
- `read` : Deferred値を作る
- `fill` : Iver.tのDeferredを埋める

```ml
let ivar = Ivar.create ()
let deferred = Ivar.read ivar

Deferred.peek deferred ;;
Ivar.fill ivar "Hello" ;;
Deferred.peek deferred;;
```

# Upon

```ml
Deferred.upon;;
- : 'a Deferred.t -> ('a -> unit) -> unit = <fun>
```

`upou <遅延値> (fun 遅延値を利用する副作用関数)`

`upon`関数は、遅延値が決定されたときに実行する副作用関数をスケジューリングする
このコールバックは、新しい`Deferred.t`を生み出さない

でも、`upon`でバインドしても
遅延値が評価されるまでにプログラムが終わったら実行されない



## 一定の遅延後に一連のアクションをスケジューリングする。


sig
```ml
module type Delayer_intf = sig
  type t

  val create : Time.Span.t -> t
  val schedule : t -> (unit -> 'a Deferred.t) -> 'a Deferred.t
end
```

module
```ml
module Delayer : Delayer_intf = struct
  type t = {delay: Time.Span.t; jobs: (unit -> unit) Queue.t}

  let create delay = {delay; jobs= Queue.create ()}

  let schedule t thunk =
    let ivar = Ivar.create () in
    Queue.enqueue t.jobs (fun () -> upon (thunk ()) (fun x -> Ivar.fill ivar x)) ;
    upon (after t.delay) (fun () ->
        let job = Queue.dequeue_exn t.jobs in
        job ()) ;
    Ivar.read ivar
end
```

実は微妙なコード
thunk(unitを引数にとる関数)を`jobs`としてキューで保存するが、その使われ方が微妙。
可能な限り、 `map, bind, return` スタイルで Deferredを扱ったほうが良い


# Ivar と upon の観点での bind を理解する

```ml
let d' = Deferred.bind d ~f
```

流れ
- 最終結果を保持する`Ivar i`を作成
- `Ivar.read i -> Deferred`を返す。
- 遅延された`d`を計算する関数`f`を登録する
- `d`が決定されたら`f`を実行
- `Ivar.fill i (f d)`
- i が埋められたので、対応する遅延値の`d'`が決定される


`Ivar.t`は上記の式にはでないが、内部的には`Deferred.bind`を使ったときに生成されている

## 自前のbind関数

自分で実装しようとすると、`Ivar`と副作用的な処理を利用することになる
```ml
let my_bind d ~f =
  let i = Ivar.create () in
  upon d (fun x -> upon (f x) (fun y -> Ivar.fill i y)) ;
  Ivar.read i
```

上記は一種のメンタルモデル
実際は多くの最適化が施され、もっと複雑なコード
だが、`upon`と`Ivar`の動作を理解するのに役立つ


# Example: An Echo Server

Standalone な Async プログラムを書く



## copy_blocks 関数
```ml
let rec copy_blocks buffer r w =
  Reader.read r buffer
  >>= function
  | `Eof -> return ()
  | `Ok bytes_read ->
      Writer.write w (Bytes.to_string buffer) ~len:bytes_read ;
      Writer.flushed w >>= fun () -> copy_blocks buffer r w
```

操作順を非同期で決めるため、bindが使われている

- `Writer`
  - `write` : 同期的に書き込む
  - `flushed` : 非同期でフラッシュする
- `Reader`
  - `read` : 非同期で読み込む
    - reader -> buffer への上書き
      - reader からは読み込んだ分が消去され、buffer の元のデータは全部上書き？

push back 方式
buffer に入ってきたバイト分だけ、 writer に書き込む
writer に書き込んだ分は、すぐに flush(出力してメモリを開放)

コレにより、入力を溜め込まないので`buffer`, `writer`のメモリ消費を節約



### Deferred Chain における Tail-call

上の`copy_blocs`関数は、再帰関数

`Reader.read`の結果の`Deferred.t`を後ろの再帰呼び出しにバインドしているので、
`Deferred.t >>= Deferred.t >>= Deferred.t >>= ...`
みたいなチェーンになっている。

このチェーンの長さには制限が無いので、無限リストと同じで
エコー処理が続くとメモリを無制限に消費すると思われるが、
`Async`には最適化のための特殊なケースが有る。

その条件は、末尾再帰と同じ
今回のパターンでは、`Eof -> return ()`でこの関数の戻り値が単一の`() Deferred.t` であることは確定しているので、
一連のDeferred Chain は一つのDeferredと安全にみなすことができる。
よって、末尾再帰最適化がされ、メモリリークにならない。


## TCP サーバー
上のEcho Serverを呼び出すために、サーバーのコードを書く

`Async.Tcp`モジュールに、色々含まれている

`Tcp.Server`, `Tcp.Client`など

```ml
open Async

let run () =
  let host_and_port =
    Tcp.Server.create ~on_handler_error:`Raise
      (Tcp.Where_to_listen.of_port 8765) (fun _addr r w ->
        let buffer = Bytes.create (16 * 1024) in
        copy_blocks buffer r w) in
  ignore (host_and_port : (Socket.Address.Inet.t, int) Tcp.Server.t Deferred.t)

let () =
  run () ;
  never_returns (Scheduler.go ())
```

`Tcp.Server.create`関数は、`('a, 'listening_on) Async.Tcp.Server.t Deferred.t`型を返す

`run`関数では、`host_and_port`関数を実行してバックエンドにサーバーDeferred値を作りつつ、
それを無視している(作って起動して終わり)。

## never_returns (Scheduler.go ())

`never_returns`関数は、`never_returns`型を返す関数を実行する関数
つまり、無限ループの関数版

## Async初心者がよくやるエラー

それは、`Schedular`を起動するのを忘れること。
`Schedular`がないと、非同期プログラムは何もできない

```ml
let () =
  run () ;
```
これだけだと、即終了する。
`Schedular.go`関数を実行することで、非同期処理のスケジュールが開始して
Deferred値が計算され始める。


## 試す

```sh
dune build
dune exec ./main.exe

echo "hello" | nc 127.0.0.1 8765
echo "world" | nc 127.0.0.1 8765

killall main.exe
```


# Function That NEVER RETURN

決して戻らない関数

`never_returns`は`Core`に由来する慣用句
return しない関数をマークするために使う

通常は、returnしない関数(無限再帰関数)などは
`'a`型としてinferされる

例.
```ml
let rec loop_forever () = loop_forever ()

let always_fail () = assert false
```



## 注釈する
```ml
let rec loop_forever (): never_returns = loop_forever ()

let always_fail (): never_returns = assert false
```

型ラベルで注釈すると良い

## 関数の中で never_returns を返そうとすると
エラーになる

```ml
let do_stuff n =
  let x = 3 in
  if n > 0 then loop_forever ();
  x + n
```
上の関数は、0より大きいと、`never_returns`を返そうとする。
するとコンパイルエラー(戻り値を `int` or `never_returns` のどちらかにしないといけない)


```ml
let do_stuff n =
  let x = 3 in
  if n > 0 then never_returns(loop_forever ());
  x + n
;;
```

`never_returns`関数に適用すると、その戻り値は無視される

明示的マークにより、型エラーを解決できる。

# Echo Serverの改良

- `Async.Cmmand`モジュールを使ってコマンドライン引数を加える
- Listenするポートを決めるフラグ、エコーする文字を大文字にするかどうかのフラグを追加
- `Async.Pipe`を使って、コードをシンプルにする


```ml
let run ~uppercase ~port =
  let host_and_port =
    Async.Tcp.Server.create ~on_handler_error:`Raise
      (Tcp.Where_to_listen.of_port port) (fun _addr r w ->
        Pipe.transfer (Reader.pipe r) (Writer.pipe w)
          ~f:(if uppercase then String.uppercase else Fn.id)) in
  ignore (host_and_port : (Socket.Address.Inet.t, int) Tcp.Server.t Deferred.t) ;
  Deferred.never ()

let () =
  Command.async_spec ~summary:"Start an echo server"
    Command.Spec.(
      empty
      +> flag "-uppercase" no_arg
           ~doc:" Convert to uppercase before echoing back"
      +> flag "-port"
           (optional_with_default 8765 int)
           ~doc:" Port to listen on (default 8765)")
    (fun uppercase port () -> run ~uppercase ~port)
  |> Command.run

```

`Command.async_spec`でコマンド引数+コールバックを`Command.t`型にして、
`Command.run`で実行

`Deferred.never ()` を返すと、never_returnsとおなじになる。


# Async.Pipe

Pipeは、非同期コミュニケーションのためのチャンネル
プログラム同士をつなぐために使う

`consumer / provider`の関係

```ml
let r, w = Pipe.create ()
let write_complete = Pipe.write w "Hello, world";;
let read_complete = Pipe.read r;;
```

`Pipe.Reader.t` と `Pipe.Writer.t` 読み書きのためのハンドル
`Pipe.create`でセットで出てくる

書き込む -> 読み込む(遅延値で取得) -> 書き込みの方の遅延値が戻る


## Pipe.transfer
`transfer`関数は、ユーティリティの一つ

`('a, Pipe.Reader.phantom) Pipe.t`型と、
`('b, Pipe.Writer.phantom) Pipe.t`型をとり、
Reader からの入力を Writer にそのまま流す

`~f`ラベル引数で、`string -> string`型の関数を受け取って
ReaderからWriterに流す間に文字を加工できる。

`Base.Fn.id`関数は、受け取った引数をそのまま帰す関数っぽい？
`uppercase`フラグが経っていないときは変わりにこっちを使う

## Command.async_spec

`async_spec`関数は、`Command.run`で実行できるコマンドを作成する。
`Command.basic`と似ているが、メイン関数として実行すべき関数が`Deferred.t`を返さないと行けない。

`Command.run`すると、`Scheduler.go`への明示的な呼び出しを必要とせず
Async Schedulerが自動でスタートする。


# 例.2 Serching Definitions with DuckDuckGo

DuckDuckGoは検索エンジン。
検索エンジンのインターフェースを使って、
用語の定義集を収集する。

### 使うライブラリのリスト

opam でインストールしておく
- textwrap : 長い行の出力を折り返すためのライブラリ
- uri : URIを扱うライブラリ。Uniform Resource Identifiers, HTTP URL も含まれる
- yojson : JSONをパースする
- cohttp : HTTPクライアント/サーバーを作る。非同期をサポートするには`cohttp-async`パッケージが必要




# URI Handling

結構好み
簡単に、クエリやパスを追加できそうな見た目

```ml
let query_uri query =
  let base_uri = Uri.of_string "http://api.duckduckgo.com/?format=json" in
  Uri.add_query_param base_uri ("q", [query])
```

# Parsing JSON String

DuckDuckGo の HTTP Responce は　RFC4627に定義される形式の
シンプルなJSONらしい

```ml

let get_definition_from_json json_str =
  match Yojson.Safe.from_string json_str with
  | `Assoc kv_list -> (
      let find key =
        match List.Assoc.find ~equal:String.equal kv_list key with
        | None | Some (`String "") -> None
        | Some s -> Some (Yojson.Safe.to_string s) in
      match find "Abstract" with Some _ as x -> x | None -> find "Definition" )
  | _ -> None
```

まずは、string を JSON にパースするところから

`Yojson.Safe.t`型は、多相バリアントの集合型らしい

```ml
module Safe = struct
type t =
    [
    | `Null
    | `Bool of bool
    | `Int of int
    | `Intlit of string
    | `Float of float
    | `String of string
    | `Assoc of (string * t) list
    | `List of t list
    | `Tuple of t list
    | `Variant of (string * t option)
    ]
...
end
```

json オブジェクトを文字列からパースする、`Yojson.Safe.from_string`を実行したら
これらの中のどれかが帰ってくる。

`Assoc of (string *t) list`の可能性が高い。
JSONとは、キーバリューのペアなので、大体この形




## HTTP Request

```ml
let get_definition word =
  Cohttp_async.Client.get (query_uri word)
  >>= fun (_, body) ->
  Cohttp_async.Body.to_string body
  >>| fun body_text -> (word, get_definition_from_json body_text)
```

`cohttp-async`を使う

## print resut

```ml

let print_result (word, definition) =
  printf "%s\n%s\n\n%s\n\n" word
    (String.init (String.length word) ~f:(fun _ -> '-'))
    ( match definition with
    | None -> "No definition found"
    | Some def -> String.concat ~sep:"\n" (Wrapper.wrap (Wrapper.make 70) def)
    )
```

`textwrap` を使う
`70文字目`もしくは改行文字のところで区切ったリストにして返す。
それをまた`\n`で連結して返す


## 検索の実行
定義を調べる単語は、リストで渡す　

```ml
let search_and_print words =
  Deferred.all_unit
    (List.map words ~f:(fun word -> get_definition word >>| print_result))
```

`Deferred.all` : Defered.t list を受け取ってまたリストで返す
`Deferred.all_unit` : `unit Deferred.t list` を受け取って、単一の`unit Deferred.t`

複数の検索単語がある場合、Deferredをチェーンして順番にやるより、
並行で実行したほうが時間効率がいい




```ml
let () =
  Command.async_spec
    ~summary:"Retrieve definitions from duckduckgo search engine"
    Command.Spec.(empty +> anon (sequence ("word" %: string)))
    (fun words () -> search_and_print words)
  |> Command.run
```

エントリーポイント
最初に説明文を表示
summaryを書く

`+>`演算子で、コマンド引数を追加していく
`name %: Arg.type` : 名前付き引数 
`anon`関数は、匿名引数を作る関数


### 試す

```sh
dune exec -- ./search.exe "Concurrent Programming" "OCaml"
```




# Exception Handling

外部リソースを使用したプログラミングに、エラーはつきもの
(外部リソースとは、Disk, Network, Server, I/O のこと)

OCamlの場合、Error-Aware return もしくは Exception で処理する。


しかし、例外がキャッチできなかった場合はプロセス全体がダウンする。
```ml

let maybe_raise =
  let should_fail = ref false in
  fun () ->
    let will_fail = !should_fail in
    should_fail := not will_fail ;
    after (Time.Span.of_sec 0.5)
    >>= fun () -> if will_fail then raise Exit else return ()

s
```

## Asyncの例外処理

### try/with

```ml
let handle_error () =
  try
    maybe_raise ()
    >>| fun () -> "success"
  with _ -> return "failure"
```
このコードでは、例外は補足できない。
`try/with`では、その中で直接実行されたコードの例外を補足する
maybe_raiseがスケジュールする非同期処理は、0.5秒後に実行されるので
補足できない

## Async.try_with 関数

```ml
let handle_error () =
  try_with (fun () -> maybe_raise ())
  >>| function 
  | Ok () -> "success"
  | Error _ -> "failure"
```

`try_with f`
thunkを取り、遅延型のResult型を返す


`unit -> Deferred.t`のthunk関数を引数として渡して
`Result.t Deferred.t`型の戻り値に変換できる
Errorの中身は`'a exn`型




# Monitor
`try_with`を始め、
すべてのAsync例外処理メカニズムは、
Erlangに実装されている`Monitor`というエラー処理メカニズムにインスパイアされている


Asyncの文脈の`Monitor`は、処理されていない例外があった時どうするか決めるコンテキスト

新しいAsync job がスケジュールされる時(bind or map)、
現在のjobのmonitorを継承した新しい monitor を spawn する。

- monitor はツリー状に並んでいる
- monitor は Deferred, またはそうではない戻り値型の thunk 関数を持っている

## monitor の例
`Monitor.t`インスタンスを作成して、
そのモニターに例外を起こしそうな非同期関数を監視させる

```ml
open Async

let blow_up () =
  let monitor = Monitor.create ~name:"blow up monitor" () in
  within' ~monitor maybe_raise
```


`Async.within'`関数は、で監視しつつ 非同期関数を実行する。
`Async.Monitor.create`でモニターが作れる



"blow up monitor"という、通常のスタックトレースに追加で例外を監視するトレースが表示される。

- 例外のエラートレースを補強
- monitorに届いたエラーを明示的に処理

## Monitor.detach_and_get_error_stream


特に重要な関数
- 親モニターからモニターを取り外す
- または、error_streamをモニターから取り外し、取得する

`Stream.iter`を実行してエラー処理関数を登録しておくことで、
そのモニターに通されたエラーはその関数で処理される。

```ml
let swallow_error () =
  let monitor = Monitor.create () in
  Stream.iter (Monitor.detach_and_get_error_stream monitor) ~f:(fun _exn ->
      printf "an error happend\n") ;
  within' ~monitor (fun () -> after (Time.Span.of_sec 0.25))
  >>= fun () -> failwith "Kaboom!"

```
swallow = 飲み込む

この関数を実行しても、例外で計算が終了するので
`Deferred`が返されることは無い。

# 機能追加
# タイムアウト、キャンセル、チョイス　

```ml
let get_definition_with_timeout ~server ~timeout word =
  Deferred.any
    [ (after timeout >>| fun () -> (word, Error "Timed out"))
    ; ( get_definition ~server word
      >>| fun (word, result) ->
      let result' =
        match result with
        | Ok _ as x -> x
        | Error _ -> Error "Unexpected failure" in
      (word, result') ) ]
```

`Deferred.any`関数は、
`Deferred.t list`をとり、最初に解決された Deferredの値を返す。
よって、`timeout deferred`と`HTTP Request deferred`をanyにリストで渡せば、
タイムアウトかリクエストの先に終わったほうが戻る。

すなわち、タイムアウト付きリクエストになる。

ただし、上記のコードはタイムアウトしても
リクエストがシャットダウンしない欠点があり、接続がメモリリークするので 
`interrupt`ラベル(割り込み)にDefferedを与え、割り込みが決定されると
接続を切るようにする

```ml

let get_definition ~server ~interrupt word =
  try_with (fun () ->
      Cohttp_async.Client.get ~interrupt (query_uri ~server word)
      >>= fun (_, body) ->
      Cohttp_async.Body.to_string body
      >>| fun string -> (word, get_definition_from_json string))
  >>| function
  | Ok (word, result) -> (word, Ok result)
  | Error _ -> (word, Error "Unexpected failure")

let get_definition_with_timeout ~server ~timeout word =
  get_definition ~server ~interrupt:(after timeout) word
  >>| fun (word, result) ->
  let result' =
    match result with Ok _ as x -> x | Error _ -> Error "Unexpected failure"
  in
  (word, result')
```

# choice & choose


`choose`関数は、
`Deferred.choice`型のリストから最初に値を返す1つの`Deferred.t`を正確に選ぶ

`choice`関数は、
遅延値が解決したときにそれを引数にとるコールバックを取る。
選ばれるときに実行する。

```ml
choice;;
- : 'a Deferred.t -> ('a -> 'b) -> 'b Deferred.choice = <fun>
choose;;
- : 'a Deferred.choice list -> 'a Deferred.t = <fun>
```


上のコードではタイムアウトを正格に処理することができない
なぜなら、`get_definition`のエラーはタイムアウトだけでなく、その他全て含めて`Unexpected failure`だから。
`choose`関数を使うことで、正しく処理できる。

```ml
let get_definition_with_timeout ~server ~timeout word =
  let interrupt = Ivar.create () in
  choose
    [ choice (after timeout) (fun () ->
          Ivar.fill interrupt () ; (word, Error "Time out"))
    ; choice
        (get_definition ~server ~interrupt:(Ivar.read interrupt) word)
        (fun (word, result) ->
          let result' =
            match result with
            | Ok _ as x -> x
            | Error _ -> Error "Unexpected failure" in
          (word, result')) ]

```
割り込みの Ivar を用意
`get_definition`には`Ivar.read interrupt`を渡す。


# any vs choose & choice

anyだと、タイムアウト時点で返す。
リクエストをキャンセルできない

choose + choice だと
遅延値を解決時に、関数を実行できるので
`Ivar`を使うことで`Time out `エラーを選択しつつ
`get_definition ~interrupt`の割り込みDeferredをfillできる





# Working with System Thread

interleave = 間隔を開ける、挟む

OCaml は `true system thread` を組み込みサポートしている。

OSによってスケジューリングされるスレッド
(kernel-level thread)



OCamlのsystem thread サポートでは、物理的な並列性にアクセスできない
OCaml Runtime は単一のランタイムロックしか持たない。
1つのスレッドだけがそれを持てる。

Asyncがあるのにシステムスレッドが必要な理由
- ノンブロッキングの代替手段が無いOSシステムコールがあるから
  - Asyncは裏でそれをラップして、スレッドプールを保持する
  - ユーザーが意識する必要はないが
- 独自のイベントループ、その他の理由で独自スレッドを必要とする `non-OCaml` ライブラリを扱うため
  - つまり、FFIで使うかもしれないから

- 計算量が多いOCamlコードの相互運用性を高める
  - Asyncでは、`bind, map`を呼び出さない長いコードが有るとAsyncランタイムをブロックする
  - 計算を小さく`bind`で分離することで解決する
    - 既存のコードベース変更が必要で、現実的でない場合がある
  - 上の理由から、スレッドをブロックしそうなコードを別のシステムスレッドで実行することで解決できる


## Async.In_thread モジュール

`In_thread.run`は、Asyncパッケージでシステムスレッドを使う最も単純な方法
別スレッドで実行し、戻り値は`Deferred.t`で他のAsyncの関数で扱える形になる

```ml
let def = In_thread.run (fun () -> List.range 1 10)
```

## Async & システムスレッドの相互運用
トリッキーになる場合がある

```ml
let log_delays thunk =
  let start = Time.now () in
  let print_time () =
    let diff = Time.diff (Time.now ()) start in
    printf "%s, " (Time.Span.to_string diff)
  in
  let d = thunk () in
  Clock.every (sec 0.1) ~stop:d print_time;
  d >>= fun () ->
  printf "\nFinished at: ";
  print_time ();
  printf "\n";
  Writer.flushed (force Writer.stdout);
```


普通に実行
```ml
log_delays (fun () -> after (sec 0.5));;
37.670135498046875us, 100.65722465515137ms, 201.19547843933105ms, 301.85389518737793ms, 402.58693695068359ms,
Finished at: 500.67615509033203ms,
- : unit = ()
```
ほぼ100ミリ秒


同期処理でブロッキング
```ml
let busy_loop () =
  let x = ref None in
  for i = 1 to 100_000_000 do x := Some i done
;;
val busy_loop : unit -> unit = <fun>
log_delays (fun () -> return (busy_loop ()));;
Finished at: 874.99594688415527ms,
- : unit = ()
```
`busy_loop()`が終わるまで、完璧に`log_delay`関数をブロック


`busy_loop`の同期処理を別スレッドで
```ml
log_delays (fun () -> In_thread.run busy_loop);;
31.709671020507812us, 107.50102996826172ms, 207.65542984008789ms, 307.95812606811523ms, 458.15873146057129ms, 608.44659805297852ms, 708.55593681335449ms, 808.81166458129883ms,
Finished at: 840.72136878967285ms,
- : unit = ()
```

途中から、１秒の桁が 7 -> 8 になっている
正確な100ms秒間隔ではなくなっている。

これは、Asyncランタイムではなく OS によってスレッドの実行がスケジューリングされるから

## つまりは
システムスレッドを使うと、
スレッドの実行間隔の予測が付きづらいということ。
(OSによってスケジューリングが行われるため)


# Thread-Safety and Locking
システムスレッドを使って作業するときは、`mutable`なデータ構造に注意

殆どのOCamlデータ構造は複数スレッドから同時アクセスされたときの十分なセマンティクスを定義されていない。
よって、常に`Mutex`を使う必要がある。

2つの一般的なMutexパッケージ

- `Mutex`
  - OSレベルMutexのラッパー
- `Nano_mutex`
  - OCamlランタイムが行うロックの一部を利用した効率的な代替品
  - OSレベルMutexを作ることを避ける
  - 上のやつよりMutex作成は20倍、取得は40%ぐらい高速


## Async と System Thread の組み合わせの基本

- thread同士でmutableな状態を共有させない
  - `Nano_mutex`を使う
- `In_thread.run`で実行する計算で、Asyncライブラリへの呼び出しを一切行わない


Asyncパッケージには、システムスレッドをAsyncにして安全に使う方法がいくつかある

`Async.Thread_safe`モジュールをｗｐ使うと、
外部スレッドのAsyncロックを取得して安全にAsync計算できる。


