# Fanctors

これまでのOCamlモジュールの役割

- コードを、インタフェースを持つユニットとして整理するメカニズム

しかし、OCamlのモジュールシステムはそれ以上の事ができる。
そのパワーはファンクターからもたらされる

## What is Fanctor?

Fanctor = モジュール　を渡して モジュール を返す関数

コーディングにまつわる以下の問題を解決

- Dependency injection
  - コードのコンポーネントの実装を交換可能にする
  - テスト、シミュレーションが簡単に
- Autoextension of modules
  - 自動拡張
  - 既存のモジュールの機能拡張の方法を標準化する
  - 機能の拡張を多くの異なる方に適用する
- Instatntiating modules with state
  - mutableな状態を含むモジュールのインスタンス化
  - 特定のモジュールで必要な副作用的な状態をサポートする

これらはほんの一部


# Trival Example


超簡単な例

普通の関数よりちょっと構文の重い関数みたいな感じ



```ml
open Base

module type X_int = sig
  val x : int
end


module Increment (M: X_int) : X_int = struct
  let x = M.x + 1
end
```

X_Int モジュールシグネチャを定義
Increment モジュールを定義

モジュール定義が、
モジュールを引数にモジュールを返す関数のような感じになっている。

普通の関数と違って、引数と戻り値に `sig`が注釈するのが`必須`

`X_int -> X_int`

正格には、引数だけ必須
戻り値を省略すると、明示的な`sig`が型推論される。
しかし、普通は出力モジュールが成約されたほうが良い

## Fanctor使ってみる

関数と違って () は省略できない
```ml
module Three = struct let x = 3 end
module Four = Increment(Three)

;;
Four.x
(* 4 *)
```


`.ml`ファイルが`.mli`ファイルのインターフェース定義を満たさないと行けないように、
Fanctorに適用するモジュールも、引数の型にあった定義にしないといけない。


ただし、インターフェース定義より余分な定義は問題ない

```ml
module Three_and_more = struct
  let x = 3
  let y = "three"
end

module Four = Increment(Three_and_more)
```

オブジェクトが与えられたインターフェースを満たすかどうかという
オブジェクト志向のルールの精神と似ている

# Computing with Interval

Interval = 区間

high と low を持つ Interval モジュールを作る。
間にある値とかを計算できる。




## Comparable Module

- t : そのデータ構造モジュールの型
- compare : t型を2つ取り、比較結果を整数値で返す

```ml
module type Comparable = sig
  type t
  val compare : t -> t -> int
end
```

## compare 関数
OCamlの慣習では、
2つの要素の差を整数値で返すのが`compare`関数

```ml
compare x y < 0     (* x < y *)
compare x y = 0     (* x = y *)
compare x y > 0     (* x > y *)
```

実は歴史的な誤りの習慣であり、本当は `Lessthan, Greaterthan, Equal`の3ケースのVariantを返すべきだが
この時点で確立されていて変更される可能性は低い




# Make_interval モジュール

```ml
module Make_interval (Endpoint : Comparable) = struct
  type t = Interval of Endpoint.t * Endpoint.t | Empty

  let create low high =
    if Endpoint.compare low high > 0 then Empty else Interval (low, high)

  let is_empty = function Empty -> true | Interval _ -> false

  let contains t x =
    match t with
    | Empty -> false
    | Interval (low, high) ->
        Endpoint.compare low x >= 0 && Endpoint.compare x high >= 0

  let intersect t1 t2 =
    let min x y = if Endpoint.compare x y <= 0 then x else y in
    let max x y = if Endpoint.compare x y <= 0 then y else x in
    match (t1, t2) with
    | Empty, _ | _, Empty -> Empty
    | Interval (l1, h1), Interval (l2, h2) -> create (max l1 l2) (min h1 h2)

end
```

- t : Interval | Empty バリアント
- create low high : 作る
- is_empty : 空かどうか
- intersect t1 t2 : 2つの区間を取って、重なる部分を返す


# モジュールのインスタンス化

Functorは
`モジュールを渡してモジュールを返す関数`

つまり、モジュールのコンストラクタ関数とも言える



Functor関数の引数で直接モジュール定義してそのまま引数として渡す
```ml
module Int_interval = Make_interval (struct
  type t = int
  let compare = Int.compare
end)
```


## Int_intervalを使ってみる
モジュールのインスタンス

```ml
let interval1 = Int_interval.create 1 10
let interval2 = Int_interval.create (-10) 20
let interval3 = Int_interval.create (-10) 0
let empty = Int_interval.create 0 (-1)

(*  *)

let is_contain = Int_interval.contains interval1 5
let intersection = Int_interval.intersect interval1 interval2
let empty2 = Int_interval.intersect interval1 interval3

```

変数や、型、比較関数、などは
Int型に特化した`Comparable`インターフェースによって具体化される


# Moduel Standard

実は、独自に`Comparable`型を満たすような定義をしなくても、
`Base`のモジュールは大体

- t型
- compare関数
を定義しているので、いろんなモジュールで Intervalを作れる

```ml
open Base

module Int_interval = Make_interval (Int)
module Float_interval = Make_interval (Float)
module String_interval = Make_interval (String)
module Char_interval = Make_interval (Char)
```

ここで使っているのは、`Base.Int`, `Base.Float` `Base.String`
であることに注意

重要なことは、Fanctor関数を定義するときは
`Base`などでよく使われるようなモジュールインターフェース
つまりシグネチャを元に作ること

標準化されたシグネチャを受け取るようなファンクタは使いやすく、標準化を促進する。


## 異なる内部実装を簡単に実装
```ml
module Rev_int_interval =
  Make_interval(struct
    type t = int
    let compare x y = Int.compare y x
end)
;;
```

Int_intervalの比較を逆にしたもの
逆区間の計算

こうして作ったものは、`Int_interval`と別の型として認識される(結構重要)



# Fanctorを抽象化する

実は、`Make_Interval`を使わずに`Int_interval.Interval`バリアントを作ることができる。

```ml
let illegal_interval = Int_interval.Interval(100, 0)
```

基本、Variantの生成は`create`関数によってのみ行われるべき

ただ、`Int_interval`のネームスペース内では
`type t = Interval of Int.t * Int.t | Empty`のように
単なる2要素Intタプルを持つバリアントであるため
Variantコンストラクタで違法な区間を作れてしまう。



## signature で抽象化

intf = interface の略？

```ml
module type Interval_intf = sig
  type t
  type endpoint

  val create : endpoint -> endpoint -> t
  val is_empty : t -> bool
  val contains : t -> endpoint -> bool
  val intersect : t -> t -> t
end
```
引数として渡されてくる予定のモジュールの型も`endpoint`として定義しておく



先程のFanctor定義では、戻り値の型を定義していなかった
Interval_intfを指定する


```ml
module Make_interval (Endpoint : Comparable) : Interval_intf = struct
  type endpoint = Endpoint.t
  type t = Interval of Endpoint.t * Endpoint.t | Empty
  ...
end
```

`type endpoint = Endpoint.t`としないと、
シグネチャ定義と合わなくなるのでエラーになる。


`type t`の具体的な実装がインターフェースから見えないので、
Variant型だろうとRecord型だろうと
モジュールを使う側からは型を直接作れなくなる。


## 抽象化した型シグネチャ

```ml
(* Int_interval *)
sig
  type t = Int_interval.t
  type endpoint = Int_interval.endpoint
  val create : endpoint -> endpoint -> t
  val is_empty : t -> bool
  val contains : t -> endpoint -> bool
  val intersect : t -> t -> t
end
```


# Sharing Constraints


Functorの戻り値にインターフェースを追加したら、
`create`関数の型が `endpoint`抽象型になってしまって
オブエジェクトを作ることすらできなくなった。

これを解決するため、
`endpoint` = `Int.t` であるということを公開する必要がある。

1つの方法は、`sharing constraint`構文を使って
ある抽象型と具体型が等しいということをコンパイラに教えること


```ml
(* 構文
  <Module_type> with type <type> = <type'>

  with ~ の部分は、andを使ってつなげて書ける
  <Module_type> with type <type1> = <type1'> and type <type2> = <type2'>
*)
```

元のモジュールの `抽象型1 = 具体型1`
みたいなイメージ。


つまりこう
```ml

module type Int_interval_intf = Interval_intf with type endpoint = int
(* 
module type Int_interval_intf =
  sig
    type t
    type endpoint = int
    val create : endpoint -> endpoint -> t
    val is_empty : t -> bool
    val contains : t -> endpoint -> bool
    val intersect : t -> t -> t
  end

*)
```
もとのintfが更新されたインターフェース(sig)が帰ってくる。

## Sharing Constraint を使う

Functor定義時の、戻りモジュール型で使う。
ここしか使い所が無い。


```ml
module type Int_interval_intf = Interval_intf with type endpoint = int

(* module Make_interval (Endpoint : Comparable) : Int_interval_intf
  = struct ... 
  
  error
  *)
```

コレはNG
Fanctor定義の時点では、`endpoint = Endpoint.t` 
戻り値型エラーになる


というか、普通に `enpoint = Endpoint.t`でいい

```ml
module Make_interval (Endpoint : Comparable) :
  Interval_intf with type endpoint = Endpoint.t = struct
```

これなら、`Int`型以外のIntervalでも、`endpoint`抽象型が具体型に置き換えられて公開される.



# Destructive Substitution
破壊的代入

Sharing Constraint の強力なバージョン

```ml
<Module_type> with type <type> := <type'>
```

with までは `sharing constraint`と同じ
`:=`セイウチ演算子で抽象型に具体型を代入する。

すると、`type`の部分が `type'`で置き換えられ、
元の抽象型は削除される

```ml
module type Int_interval_intf = Interval_intf with type endpoint := int
(* 
sig
  type t
  val create : Base.int -> Base.int -> t
  val is_empty : t -> Base.bool
  val contains : t -> Base.int -> Base.bool
  val intersect : t -> t -> t
end
*)
```

`endpoint` の部分がせんぶ `int`に置き換わっている。
こっちを使ったほうが良い

`type endpoint = Endpoint.t`のような、抽象型に具体型を割り当てるタイプ定義もいらなくなる

```ml
module Make_interval (Endpoint : Comparable) :
  Interval_intf with type endpoint := Endpoint.t = struct
  
  ...

end
```

# いったんまとめ

- Functor
  - モジュールを引数にしてモジュールを返す関数
    - 引数型は必須、戻り値型は任意
    - ただし、内部のデータ構造のコンストラクタを隠蔽し、関数によってのみ作成するためには戻り値のインターフェースを管理する必要あり
      - `Module.t`を抽象型に置き換える
      - 引数のモジュールも、`endpoint`に置き換える
- Sharing Constraint
  - `ModuleType with type1 = type1' ...`
    - 型シグネチャ内の抽象型を、具体型に置き換える
    - ただし、Module定義側で `type abstruct_type = concrete_type`を書かないといけない
- Destructive Substitution
  - 破壊的代入
  - `sharing constraint`の構文で`:=`セイウチ演算子を使うだけ
  - `抽象型`の部分をすべて削除、`具体型`で置き換える
    - 別に破壊的ではないのでミスリードな名前だが、既存のsigを変更して新しいsigを作成する方法の一つに過ぎない




# Multiple Interface

Functorで作成するモジュールに、
他のインターフェースも実装してみる。

S式への`serealize`機能をつける

## S-expression
S式は、Baseでよく使われるシリアライズフォーマット
文字列を元素とする、()で囲まれた式

```ml
Sexp.List [ Sexp.Atom "This"; Sexp.Atom "is"
; Sexp.List [Sexp.Atom "an"; Sexp.Atom "s-expression"]]
```

- `Sexp.List` : S式のリスト
- `Sexp.Atom` : S式のリストの要素

リストの中にリストも入れられる

## ppx_jane

`Base`におけるS式は、`ppx_jane`プリプロセッサを有効にして使う前提。
他の便利な拡張機能も使える

ppx_janeはいろんなプリプロセッサのセット
- ppx_sexp_conv
- etc...


## `[@@deriving sexp]`

任意の型に、S式への変換関数を生成する

```ml
#require "ppx_jane";;
type some_type = int * string list [@@deriving sexp];;

(* autoderiving *)
type some_type = int * string list
val some_type_of_sexp : Sexp.t -> some_type = <fun>
val sexp_of_some_type : some_type -> Sexp.t = <fun>
```
2つの関数が自動で実装されている
`**_of_**`


## Endpoint.t を シリアライズする

```ml
  type t = Interval of Endpoint.t * Endpoint.t | Empty [@@deriving sexp]

  ...
  ;;
Line 24, characters 23-33:
Error: Unbound value Endpoint.t_of_sexp
```

`[@@deriving sexp]`は、その型のモジュールが複合型(Variant, Record)だった場合、
構成要素の最小単位の型が`sexp_of_t`を実装していないと自動で実装できない
(Int, bool, String, Float, List ...)
つまり、`Endpoint.t`だったら `Endpoint.sexp_of_t` が定義されていないといけない。



## Sexpable Interface
`Base.Sexpable`


```ml
(* Base.Sexpable *)
module type Sexpable = sig
  type t
  val sexp_of_t : t -> Sexp.t
  val t_of_sexp : Sexp.t -> t
end
```

コレを Interval module のインターフェースの定義に組み込む

`include`を使って統合する

例.
```ml
module type Interval_intf_with_sexp = sig
  type t
  include Interval_intf with type t:= t
  include Core_kernel.Sexpable with type t := t
end
```

破壊的代入で、インターフェースの抽象型を同じ抽象型に上書きしておく



# 引数モジュールを、Sexpableを含むものにする
上の例は、戻り値と引数の型を統合しているようなもん
実際は意味ない

ここでは、`Endpoint`モジュール型に
- Comparable
- Sexpable
の2つのインターフェースを含みたい

```ml
module Make_interval (Endpoint : sig
  type t
  include Comparable with type t := t
  include Core_kernel.Sexpable with type t := t
end) : Interval_intf with type endpoint := Endpoint.t = struct
  type t = Interval of Endpoint.t * Endpoint.t | Empty [@@deriving sexp]

  ...
```

ので、引数のモジュールの型注釈を`sig`で定義する
コレにより、`[@@deriving sexp]`が実装できる様になる。

let定義で、モジュール内に
- `t_of_sexp`
- `sexp_of_t`
の2つを定義したのと同じ状態
トップレベルだったらトップレベルの関数になる。




# Extending Modules

ここまでは汎用的なモジュールを作る関数を作る方法

もう一つの使い方は、既存のモジュールを拡張すること


FIFOキューを拡張してみる

# Fqueue Module

Queue といえば、FIFOな構造のモジュール
先入れ先出しの内部状態を持つデータ構造

Functional Programing での文脈的に、
キューを更新した値を新しい値として返すという
関数型のmutableなアプローチにしてみる

更新した値を新しい値として返すので、
元のデータ自体は変更されない

```ml
type 'a t

val empty : 'a t

(** [enqueue q el] adds [el] to the back of [q] *)
val enqueue : 'a t -> 'a -> 'a t

(** [dequeue q] returns None if the [q] is empty, otherwise returns
    the first element of the queue and the remainder of the queue *)
val dequeue : 'a t -> ('a * 'a t) option

(** Folds over the queue, from front to back *)
val fold : 'a t -> init:'acc -> f:('acc -> 'a -> 'acc) -> 'acc
```





## implementation

`Fqueue.t = 'a list * 'a list`
左が入力リスト、右が出力リスト

なぜこんな仕組みにするかというと、
ListはArrayと違って、以下の特徴があるため
- 先頭へのアクセス、追加は早い
- 最後尾へは 要素数分の計算量がかかる

`dequeue`のタイミングで、`out_list`が空かつ`in_list`の中身があったら
in_list = 空
out_list = List.rev in_list
のように反転した状態で in_list を out_list に補充した形にする。

データへのアクセス効率が高い

見た目上のデータの先頭は、`out_list`の最初の要素になる


- enqueue : 入力リストに要素を追加
- dequeue : 出力リストから要素を一つ取る
  - 空になった時点で in_listから補充
  - option戻り値
  - in/out どっちもemptyのときはNone

```ml
module Fqueue = struct
  type 'a t = 'a list * 'a list

  let empty = ([], [])
  let enqueue (in_list, out_list) x = (x :: in_list, out_list)

  let dequeue (in_list, out_list) =
    match out_list with 
    | hd :: tl -> Some (hd, (in_list, tl))
    | []->
      match List.rev in_list with 
      | [] -> None 
      | hd :: tl -> Some (hd, ([], tl))

  let fold (in_list, out_list) ~init ~f =
    let after_out = List.fold ~init ~f out_list in 
    List.fold_right ~init:after_out ~f:(fun x acc -> f acc x) in_list
end
```

foldが少し特殊
- out_list を fold した結果を `after_out`にバインド
- `after_out`をaccの初期値にして `in_list` を List.fold_right
  - `in_list`は先頭がQueue全体としては列の最後


## 実装したFqueueの問題点

skeltal = 骨格、最低限

実装が skeltal すぎる。

`List`モジュールには、たくさんの ヘルパー関数があり、
`Fqueue.t`もシーケンスである点ではListと変わりないのに
それを利用することができない。

List.
- iter
- for_all
- ...

これらをいちいち実装し直すのはめんどい。
しかし、これらの多くは`fold`関数のシステム的な派生に過ぎないので、
新しいコンテナタイプを作ったらとりあえず`fold`関数を実装しとけば
これらを使うことができる。

## Foldable Module

Foldable モジュール (ファイル)を作り、
シグネチャを定義

Signature は、モジュールの型に過ぎないので、
モジュール定義の一部分にして他のプログラムから利用できる
(型定義と同じ様に)


```ml
module Foldable = struct
  module type S = sig
    type 'a t

    val fold : 'a t -> init:'acc -> f:('acc -> 'a -> 'acc) -> 'acc
  end

  module type Extension = sig
    type 'a t

    val iter : 'a t -> f:('a -> unit) -> unit
    val length : 'a t -> int
    val count : 'a t -> f:('a -> bool) -> int
    val for_all : 'a t -> f:('a -> bool) -> bool
    val exists : 'a t -> f:('a -> bool) -> bool
  end

  module Extend (Arg : S) : Extension with type 'a t := 'a Arg.t = struct
    open Arg

    let iter t ~f = fold t ~init:() ~f:(fun () a -> f a)
    let length t = fold t ~init:0 ~f:(fun acc _ -> acc + 1)

    let count t ~f =
      fold t ~init:0 ~f:(fun count x -> count + if f x then 1 else 0)

    exception Short_circuit

    let for_all c ~f =
      try
        iter c ~f:(fun x -> if not (f x) then raise Short_circuit) ;
        true
      with Short_circuit -> false

    let exists c ~f =
      try
        iter c ~f:(fun x -> if f x then raise Short_circuit) ;
        false
      with Short_circuit -> true
  end
end
```


順番に見ていく

### Foldable.S
Signatureの定義

```ml
  module type S = sig
    type 'a t
    val fold : 'a t -> init:'acc -> f:('acc -> 'a -> 'acc) -> 'acc
  end
```

t がコレクション型
'a が中身の型

`fold`関数で適用する関数の方では、`t`が外れて `'a` の中身だけの方になっているのがみそ

### Foldable.Extension

拡張したされたモジュールの持つSignature

```ml
module type Extension = sig
  type 'a t

  val iter : 'a t -> f:('a -> unit) -> unit
  val length : 'a t -> int
  val count : 'a t -> f:('a -> bool) -> int
  val for_all : 'a t -> f:('a -> bool) -> bool
  val exists : 'a t -> f:('a -> bool) -> bool
end
```
Fanctor関数で拡張した戻り値のモジュールのインターフェースとして使われる。

### Foldable.Extend()

ファンクター
関数らしく、動詞系のネーミング

```ml
module Extend (Arg : S) : Extension with type 'a t := 'a Arg.t = struct
  open Arg

  let iter t ~f = fold t ~init:() ~f:(fun () a -> f a)
  let length t = fold t ~init:0 ~f:(fun acc _ -> acc + 1)

  let count t ~f =
    fold t ~init:0 ~f:(fun count x -> count + if f x then 1 else 0)

  exception Short_circuit

  let for_all c ~f =
    try
      iter c ~f:(fun x -> if not (f x) then raise Short_circuit) ;
      true
    with Short_circuit -> false

  let exists c ~f =
    try
      iter c ~f:(fun x -> if f x then raise Short_circuit) ;
      false
    with Short_circuit -> true
end
```

`Arg.fold`を使って、5つの関数を拡張する。
モジュール内`open`を使って、引数モジュール名を省略して呼び出してる。

`for_all`と`exist`では、
例外をraiseして途中抜けするテクニックを使っている。

こういう、プログラムを途中抜けしたりみたいなのは命令形言語のテクニックに近い
(for, while の途中の break, continue みたいな)



## 実際に拡張してみる

includeを使うと良い

```ml
module ExtendFQueue = struct
  include Fqueue
  include Foldable.Extend(Fqueue)
end
```

または、OCamlは変数名や型名のシャドーイングに対応しているので、
同じ名前のまま拡張することもできる。
```ml
module Fqueue = struct
  include Fqueue
  include Foldable.Extend(Fqueue)
end
```

拡張したモジュールの関数・変数は普通に
元のやつと合わせて使える
```ml
let em = Fqueue.empty
let aa = Fqueue.iter em ~f:(fun x -> ())
```


# Base の モジュール拡張Fanctor

色々種類がある
それぞれ、引数のインターフェースに合う関数さえ実装していれば、
簡単に関数を拡張できる。


`Base.`
- `Container.Make`
  - 今回作成した`Foldable.Extend`ファンクターに似てる
- `Comparable.Make`
- `Hashble.Make`
- `Monad.Make`

Baseで一般的に使える機能を
独自のデータ構造に追加したいときに便利

# まとめ
ファンクターのできること

- 汎用的な型のモジュールの作成
  - Make_interval() ファンクター
    - Int_interval
    - Float_interval
    - String_interval

- 既存のモジュールの拡張
  - Fqueue
  - Foldable.Extend() ファンクター

  - `Base.****.Make`


