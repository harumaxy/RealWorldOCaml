# Classess

Object志向の目的
- カプセル化
- 継承
  - コードの再利用

継承するために、Classを導入する必要がある。

Classは、オブジェクトを作るためのRecipe


# 簡単なクラス


```ml
open Base

class istack =
  object
    val mutable v = [0; 2]

    method pop =
      match v with
      | hd :: tl ->
          v <- tl ;
          Some hd
      | [] -> None

    method push hd = v <- hd :: v
  end

```
コレを評価すると、
`class istack : object ... end` を返す (`< ... >`ではなく)
sigっぽい


module と module type (sig)が完全に分離されているように、
object と class も分離されている

上の例は、`<pop : int; push : int -> unit>`のような
オブジェクト型を示さない

`object type != class` !




# new キーワード
`new class` でオブジェクトをインスタンス化する


# class のインスタンスの方

```ml
let s = new istack
(* val : istack = obj *)
```
`istack`型と表示されている
実際は、クラス定義と同時にオブジェクト型のエイリアスも定義されているだけ

```ml
#typeof "istack";;
type nonrec istack = < pop : int option; push : int -> unit >
```

istack クラスを new するとこの型を持つが、
この型を持つからといって `istack`から作られたとは限らない


# Class Prameter

クラス引数
インスタンス化するときに渡す引数

OCamlのオブジェクトには`constructer`メソッドが無いので、多分それの代わり

- 型引数
- 初期化引数
の2つがある

`[]` で型変数を囲むのが特徴
初期化引数は普通の関数の引数みたいな感じ

syntax
```ml
class ['a ,...] stack init ... = 
  object ... end
```



```ml
class ['a] stack init =
  object
    val mutable v : 'a list = init

    method pop =
      match v with
      | hd :: tl ->
          v <- tl ;
          Some hd
      | [] -> None

    method push hd = v <- hd :: v
  end


let s = new stack ['a'; 'b'; 'c']

```

`new`するときに引数を与える
型パラメータでポリモーフィック

型制約ラベルは、コンパイラが正しい方を推測するのに必要
エラーが出たら、型変数や型ラベルを見直すと良いかも


# Object type as Interfacce

オブジェクト型はインターフェースとして使える。

stack オブジェクトに `Iterator` インターフェースを追加して、`iter`メソッドが使えるようにする



## interface 定義

JavaスタイルとC++スタイル両方のインターフェースをサポートしているらしい


1. `'a iterator` オブジェクト型を定義、それに沿って list_iterator class を実装
```ml
type 'a iterator = < get: 'a ; has_value: bool ; next: unit >

class ['a] list_iterator init =
  object
    val mutable current : 'a list = init

    method has_value = Poly.(current <> [])

    method get =
      match current with
      | hd :: _ -> hd
      | [] -> raise (Invalid_argument "no value")

    method next =
      match current with
      | _ :: tl -> current <- tl
      | [] -> raise (Invalid_argument "no value")
  end

```

2. stack のメソッドに組み込む

```ml
class ['a] stack init =
  object
    val mutable v : 'a list = init

    ...

    method list_iterator : 'a iterator = new list_iterator v
  end

```

3. 使ってみる

```ml
let s = new stack [];;
s#push 5
s#push 4
let it = s#iterator;;

it#get
it#next
it#get
...
```

# Functional iterator

多くのOCamlプログラマは、Functional Styleを使って
イテレータオブジェクトを使わないようにしている

```ml
class ['a] stack init =
  object
  ...
    (* method list_iterator : 'a iterator = new list_iterator v *)
    
    method iter f = List.iter ~f v
  end
```

オブジェクト使わなくても、`List.iter`関数を使えばイテレーターできる


# b.

methodのローカル抽象型
methodの型ラベルの一番最初に書く

stack クラスに `fold`メソッドを実装するために必要らしい

`'b.` は `for all 'b`と読むらしく、
List.fold の `~init`の型に相当する


```ml
class ['a] stack init = object
  val mutable v : 'a list = init
  ...
  method fold f init = 
        List.fold ~f ~init v
```
こうかくとだめらしい
~initの型が bound されていないと出る。

メソッドのローカル抽象型を導入する

```ml
class ['a] stack init = object
  val mutable v : 'a list = init
  ...

  method fold : 'b. ('b -> 'a -> 'b) -> 'b -> 'b =
    (fun f init -> List.fold ~f ~init v)
end
```


# Inheritance

`inherit`キーワードを使って、
他のクラスをオブジェクト内で継承できる


```ml
class sstack init =
  object
    inherit [string] stack init

    method print = List.iter ~f:Stdio.print_endline
  end
```

構文
`inherit [型パラメータ] class-name initial-values`

上の例では、`print`メソッドが拡張されている (sstack = string stack?)


## Override & as

継承した親クラスのメソッドはオーバーライドできる

```ml

class double_stack init =
  object
    inherit [int] stack init

    method push hd = super#push (hd * 2)
  end
```

上のコードはコンパイルできない

継承した親クラスのメソッドを使いたい場合、 `as`キーワードで別名をつける
メソッドだけで、実際はオブジェクトではないらしい

```ml
class double_stack init =
  object
    inherit [int] stack init as super

    method push hd = super#push (hd * 2)
  end
```


##  継承するためにクラスを公開する

クラスは、`type`キーワードで別名を付けられる


```ml
type 'a t = 'a stack

(* type t = stack  型パラメータが足りないのでエラー *)
```


オブジェクトを公開するために、
モジュールスタイルの定義にしてみる

```ml
module Stack = struct
  class ['a] stack init = object
    ...
  end

  type 'a t = 'a stack

  let make init = new stack init
end
```

更に、`.mli`を追加して、公開するインターフェースを更に制限する

```ml
module AbstractStack : sig
   type 'a t = < pop: 'a option; push: 'a -> unit >

   val make : unit -> 'a t
end = Stack
```

しかし、`.mli`ファイルからクラスの情報を隠すと
利用するにはシンプルだが、継承ができない。

# Class type 

classの情報が見えるようにした `sig` がこれ

```ml
module VisibleStack : sig

  type 'a t = < pop: 'a option; push: 'a -> unit >

  class ['a] stack : object
    val mutable v : 'a list
    method pop : 'a option
    method push : 'a -> unit
  end

  val make : unit -> 'a t
end = Stack
```


`sig`のなかでは、`val`, `type`の他に
`class`も使えて、その定義内容でクラスの型をを記述する。

`Class Type`(クラス型)は主流のオブジェクト言語には存在しない
(Rust や Haskell の型クラスとは違うのか?)


# Open Recursion

オブジェクトのメソッドが、他のオブジェクトのメソッドから呼び出せる
dynamic lookup で実行時に動的に検索される。

あるクラスを継承した子クラスの場合、
別クラスのメソッドも呼び出せる。

よって、相互再帰を別々に定義できる
(Functional style だったら let ... and で連続で定義しないといけないところ)


クラス以外でやろうとすると冗長になる。

## Document format type



```ml
type doc =
  | Heading of string
  | Paragraph of text_item list
  | Definition of string list_item list

and text_item =
  | Row of string
  | Bold of text_item list
  | Enumerate of int list_item list

and 'a list_item = {tag: 'a; text: text_item list}
```
関数型スタイルの代数的データ型で書くとこう

これらを再帰的に巡回する関数を書くのは簡単
ただ、似たような再帰関数をたくさん書かなければいけない場合どうするか
共通部分を抜き出して、繰り返しを避けるにはどうするか


最も簡単な方法が、クラスで open recursion を利用すること
文書を格納するクラス


```ml
open Core

class ['a] folder =
  object (self)
    method doc acc =
      function
      | Heading _ -> acc
      | Paragraph text -> List.fold ~f:self#text_item ~init:acc text
      | Definition list -> List.fold ~f:self#list_item ~init:acc list

    method list_item : 'b. 'a -> 'b list_item -> 'a =
      fun acc {tag; text} -> List.fold ~f:self#text_item ~init:acc text

    method text_item acc =
      function
      | Raw _ -> acc
      | Bold text -> List.fold ~f:self#text_item ~init:acc text
      | Enumerate list -> List.fold ~f:self#list_item ~init:acc list
      | Quote doc -> self#doc acc doc
  end
```


# object(self)

`object(変数名) ... end`
で自己参照子を定義できる


逆に、定義しないと自己参照できない


## override

ふと文字のテキストアイテムを数える機能を追加したクラスに派生する

```ml
class counter =
  object
    inherit [int] folder as super

    method list_item acc li = acc

    method text_item acc ti =
      let acc = super#text_item acc ti in
      match ti with Bold _ -> acc + 1 | _ -> acc
  end

let count_doc = (new counter)#doc
```
(new class)#method

一時的にオブジェクトを作り出して、メソッドを使う



# Private Method

メソッドはプライベートにできる。
サブクラスから呼ばれる以外に、外部からアクセスできず
内部だけで利用される。

```ml
class ['a] folder2 = object(self)
  method doc acc = function
  | Heading str -> self#heading acc str
  | Paragraph text -> self#paragraph acc text
  | Definition list -> self#definition acc list

  method list_item: 'b. 'a -> 'b list_item -> 'a =
    fun acc {tag; text} ->
      List.fold ~f:self#text_item ~init:acc text

  method text_item acc = function
  | Raw str -> self#raw acc str
  | Bold text -> self#bold acc text
  | Enumerate list -> self#enumerate acc list
  | Quote doc -> self#quote acc doc

  method private heading acc str = acc
  method private paragraph acc text =
    List.fold ~f:self#text_item ~init:acc text
  method private definition acc list =
    List.fold ~f:self#list_item ~init:acc list

  method private raw acc str = acc
  method private bold acc text =
    List.fold ~f:self#text_item ~init:acc text
  method private enumerate acc list =
    List.fold ~f:self#list_item ~init:acc list
  method private quote acc doc = self#doc acc doc
end

let f :
  < doc : int -> doc -> int;
    list_item : 'a . int -> 'a list_item -> int;
    text_item : int -> text_item -> int >  = new folder2
```

`method private f = ...`


## private method on type

オブジェクト型からは隠されるが、
クラス型には含まれる。

なので、プライベートメソッドを実装すると、
継承したサブクラスでは利用できる。

## サブクラスからも隠す

privateメソッドを、サブクラスからさえも隠したい場合は、
`class`の`sig`を、`method private`のものを除いたインターフェースにする。



# Binaly Method
自分のタイプを引数にするメソッド

```ml
class square w = object(self : 'self)
  method width = w
  method area = Float.of_int (self#width * self#width)
  method equals (other : 'self) = other#width = self#width
end
```

自己参照子を定義するところで、型パラメータを導入できる。
それを、メソッドの引数の型ラベルに使う

`object(self: 'self)`

上の例では、`square#equals`メソッドで
別の図形を取って幅が同じかどうかを比較する

```ml
(new square 5)#equals (new square 5);; (* true *)
```

ただし、Binaly Method を使うとクラスのサブタイプに問題が起こる


代わりに、Poly.(=)と `repr : shape_repr`メソッドを導入して
形の種類をバリアントで表現してオブジェクトの比較を行う

```ml
type shape_repr =
  | Square of int
  | Circle of int

type shape = < repr : shape_repr; equals : shape -> bool; area : float >

class square w = object(self)
  method width = w
  method area = Float.of_int (self#width * self#width)
  method repr = Square self#width
  method equals (other : shape) = Poly.(=) other#repr self#repr
end
```


オブジェクト同士の比較では、`Poly.(=)`は動作がちょっとおかしい。
Variantなら大丈夫


# Viratual Class
Interface(Class type)を定義して、それに合うように実装するのがJava流

メソッドやフィールドが宣言されているだけで何も実装していない抽象クラスを用意して、
それを継承して Overrideするのが C++流

画面描写のため`async_graphics`パッケージを使う
macOSにはデフォルトで X11 ライブラリが入ってないので、brew で入れる

```sh
brew install Caskroom/cask/xquartz
```


`async_graphics`をduneに追加
一回`dune build`して補完が効くようにする


```ml
(executable
 (name main)
 (libraries core core_kernel ppx_sexp_conv async_graphics)
 (preprocess
  (pps ppx_sexp_conv)))
```


# drawable

Drawableオブジェクトの型を定義しておく

```ml
open Core
open Async
open Async_graphics

type drawable = < draw: unit >
```

# shape, square & circle

shape 親クラス、virtual class

square & circle 子クラス、sub class

```ml
class virtual shape x y =
  object (self)
    method virtual private contains : int -> int -> bool

    val mutable x : int = x
    val mutable y : int = y

    method x = x
    method y = y

    method on_click ?start ?stop f =
      on_click ?start ?stop (fun ev ->
          if self#contains ev.mouse_x ev.mouse_y then f ev.mouse_x ev.mouse_y)

    method on_mousedown ?start ?stop f =
      on_mousedown ?start ?stop (fun ev ->
          if self#contains ev.mouse_x ev.mouse_y then f ev.mouse_x ev.mouse_y)
  end
```

図形に共通する性質を、上のような下層クラスで定義
といっても、仮想化しているメソッドは `contains`だけ

マウスを押した/離したときのメソッドは
`contains`に依存するが、この実装の中身だけサブクラスに委ねられる


`class virtual ...`で仮想クラス
`method virtual ...`で仮想メソッドを作る
仮想メソッドはシグネチャだけ定義する

仮想クラスはインスタンス化できない

引数が宣言されているが、適用されていないファンクターのようなもの




# class ... = let ... in object ... end
`class name = <let ... in>  object ... end`

class定義のときに、objectの定義前に `let in`式を入れると、
任意の式を評価できる。　

オブジェクト作成前に実行されるので、オブジェクト本体を参照できない

# Initializer

インスタンス化中に、オブジェクトのメソッドを使用する必要がある場合に、
`initializer`を使うことができる

syntax
```ml
class name = object
  initializer expr
end
```
exprの中では、メソッドを使った式を実行できる。
OOPのコンストラクタみたいな感じ?


クリックしたら、円が拡大するハンドラーが仕込まれた派生クラスを実装する

```ml
class growing_circle r x y =
  object (self)
    inherit circle r x y

    initializer self#on_click (fun _x _y -> radius <- radius * 2)
  end
```


# 多重継承
多重継承ができる


## How Names Are Resolved

基本的に、継承、多重継承やOverrideを行うと、
最後に宣言したものが採用される
(let shadowing と同じ感じ)


```ml
class square_outline w x y = object
  inherit square w x y
  method draw = draw_rect x y width width
end
```
squareにもdrawが定義されているが、
コレはサブクラスにオーバーライドされる

```ml
class square_outline w x y = object
  method draw = draw_rect x y w w
  inherit square w x y
end
```

これは、drawの定義よりあとに inherit しているので、
サブクラスの draw が super class の draw にオーバーライドされる

多重継承もだいたい同じ


# Mixin

多重継承は過度に複雑なので、継承ではなく Composition パターンを使えという議論もある。

多重継承したいときは、 Mixin が優れていると言われる

Mixin とは => 
特定の機能ベースでメソッドを実装した仮想クラス
それ自体をインスタンス化せず、外のクラスに継承して機能を混ぜることを目的とする。


例.1　draggable
```ml
class virtual draggable = object(self)
  method virtual on_mousedown:
    ?start:unit Deferred.t ->
    ?stop:unit Deferred.t ->
    (int -> int -> unit) -> unit
  val virtual mutable x: int
  val virtual mutable y: int

  val mutable dragging = false
  method dragging = dragging

  initializer
    self#on_mousedown
      (fun mouse_x mouse_y ->
         let offset_x = x - mouse_x in
         let offset_y = y - mouse_y in
         let mouse_up = Ivar.create () in
         let stop = Ivar.read mouse_up in
         dragging <- true;
         on_mouseup ~stop
           (fun _ ->
              Ivar.fill mouse_up ();
              dragging <- false);
         on_mousemove ~stop
           (fun ev ->
              x <- ev.mouse_x + offset_x;
              y <- ev.mouse_y + offset_y))
end
```

この Mixin を、
- `mutable`な x, yフィールド
- `on_mousedown`メソッド
をもつオブジェクトに継承すると、
`ドラッグできる`機能をオブジェクトに追加できる


```ml
class small_square = object
  inherit square 20 40 40
  inherit draggable
end
```

多重継承した場合、あとに継承したものの名前空間のシンボルが
既に定義してあるものを上書きするが、
仮想メソッド、仮想プロパティの場合、


例.2 animated
```ml
class virtual animated span = object(self)
  method virtual on_click:
    ?start:unit Deferred.t ->
    ?stop:unit Deferred.t ->
    (int -> int -> unit) -> unit
  val mutable updates: (int -> unit) list = []
  val mutable step = 0
  val mutable running = false

  method running = running

  method animate =
    step <- 0;
    running <- true;
    let stop =
      Clock.after span
      >>| fun () -> running <- false
    in
    Clock.every ~stop (Time.Span.of_sec (1.0 /. 24.0))
      (fun () ->
         step <- step + 1;
         List.iter ~f:(fun f -> f step) updates
      )

  initializer
    self#on_click (fun _x _y -> if not self#running then self#animate)
end
```

virtual methodはもはや1つだけ
- `on_click`関数
を実装しているオブジェクトに継承することで、アニメーション機能を追加する。

`initializer`で`on_click`へ関数を登録している

```ml
class my_circle = object
  inherit circle 20 50 50
  inherit animated Time.Span.second
  initializer updates <- [fun _ -> x <- x + 5]
end
```








# 描写する

```ml

let main () =
  let shapes =
    [ (my_circle :> drawable)
    ; (new my_square 50 350 :> drawable)
    ; (new my_square 50 200 :> drawable)
    ; (new growing_circle 20 70 70 :> drawable) ] in
  let repaint () =
    clear_graph () ;
    List.iter ~f:(fun s -> s#draw) shapes ;
    synchronize () in
  open_graph "" ;
  auto_synchronize false ;
  Clock.every (Time.Span.of_sec (1.0 /. 24.0)) repaint

let () = never_returns (Scheduler.go_main ~main ())
```

main関数を定義

1. shapesを作る
- サブタイプを`drawable`にアップキャスト
- drawメソッドは、`async_graphics`のキャンバスに図形を描写する。
2. `repaint` 関数を定義
- 時間毎に実行
  1. `clear_graph`関数でキャンバスをクリア
  2. shapesをiterして、`drawable#draw`をすべて実行
  3. `synchronize`関数を実行すると、描写した分を同期する？

3. グラフを開く
4. 自動同期をオフ
5. 一定間隔で`repaint`を実行


```ml
let () = never_returns (Scheduler.go_main ~main ())
```

非同期処理を含む関数を実行するときは
`Scheduler.go_main`関数でメイン関数を実行する。

