# Maps, HashTables

多くのプログラミングの問題では
キーと値のペアとして構成されるデータを扱う必要がある

## 最もシンプル : Association List


```ml
open Base

let digit_alist =
  [ (0, "zero"); (1, "one"); (2, "tow"); (3, "three"); (4, "four"); (5, "five")
  ; (6, "six"); (7, "seven"); (8, "eight"); (9, "nine") ]

let a = List.Assoc.find ~equal:Int.equal digit_alist 9

```

単なる`(int * string) list`


`List.Assoc`モジュールで、検索とか操作ができる
基本的に検索はリストの長さの平均値になるので、多分効率が悪い

コレを改善したデータ構造が、`Map` or `Hashtbl`


# Maps

4章で、単語の頻出度数を出力するモジュールを実装した。

```ml
module type counter = sig
  type t

  val empty : t
  val touch : t -> string -> t
  val to_list : t -> (string * int) list
end
```

Mapを使って実装し直すともっと簡単


```ml
module Map = struct
  type t = (string, int, String.comparator_witness) Map.t

  let empty = Map.empty (module String)
  let to_list t = Map.to_alist t

  let touch t s =
    let count = match Map.find t s with Some x -> x | None -> 0 in
    Map.set t ~key:s ~data:(count + 1)
end
```

型パラメータを適用
`Map.empty`はキー型の`First Class Module`を引数に取る
このFCMには以下の２つが含まれている必要がある
- 比較関数
- S式コンバーター

Baseの標準的なモジュールなら、Mapのキー、値にできる


# Set

重複しないList
ある意味、データを無視したMap

```ml
Set.of_list (module Int) [1; 1; 2; 3] 
|> Set.to_list

(* [1;2;3] *)
```

List -> Set -> List
の順に変換すると、重複のないリストに戻る

Setにも`iter`や`map`はあるが、listに直すと汎用性が高いかも
`List.filter`で重複削除するより`Set`に変換した方がアルゴリズム的に早いかも

また、Set特有の機能として
- union
- intersect
- difference
といった論理集合演算の関数がある


# Modules and Comparators


```ml
let digit_alist =
  [ (0, "zero"); (1, "one"); (2, "tow"); (3, "three"); (4, "four"); (5, "five")
  ; (6, "six"); (7, "seven"); (8, "eight"); (9, "nine") ]

let digit_map = Map.of_alist_exn (module Int) digit_alist
let res = Map.find digit_map 3
```

`Map.of_alist_exn`で、連想リストからMapを作る。
`exn`とついてるとおり、キーが複数回重複したらException を raise

最初に`First Class Module`を引数にとっている

```ml
Map.of_alist_exn;;
- : ('a, 'cmp) Map.comparator -> ('a * 'b) list -> ('a, 'b, 'cmp) Map.t =
<fun>
```

`Map.comparator`という型を取るが、
実際は、`Base.Comparator.S`型という
FCM型のエイリアス

```ml
(* Map.of_alist_exn の signature *)
Map.of_alist_exn;;
- : ('a, 'cmp) Map.comparator -> ('a * 'b) list -> ('a, 'b, 'cmp) Map.t =
<fun>

#show Base.Comparator.S;;
module type S =
  sig
    type t
    type comparator_witness
    val comparator : (t, comparator_witness) Comparator.t
  end
```

- t : type
- comparator_witness : 比較関数の型
- comparator : Comparator.t 型

## 新しく comparator を作る

Baseの標準モジュールでないモジュールで、インターフェースを満たしたい


```ml
module Book = struct
  type t = {title: string; isbn: string}

  let compare t1 t2 =
    let cmp_title = String.compare t1.title t2.title in
    if cmp_title <> 0 then cmp_title else String.compare t1.isbn t2.isbn

  let sexp_of_t t : Sexp.t = List [Atom t.title; Atom t.isbn]
end
```

- compare関数
- sexp_of_t関数
この2つを実装しておけば、Mapのキーにできる。
と思いきやできない。

`Base.Comparator.S`型を満たす必要がある。
つまり、`sig`に合うモジュールを定義しなければならない。



```ml
module Book = struct
  module T = struct
    type t = {title: string; isbn: string}

    let compare t1 t2 =
      let cmp_title = String.compare t1.title t2.title in
      if cmp_title <> 0 then cmp_title else String.compare t1.isbn t2.isbn

    let sexp_of_t t : Sexp.t = List [Atom t.title; Atom t.isbn]
  end

  include T
  include Base.Comparator.Make (T)
end

(* 作れる *)
let new_map = Map.empty (module Book)
```

幸いにも、`T.compare`と`T.sexp_of_t`を持つモジュールは
`Base.Comparator.Make`ファンクターによって、Comparatorモジュールを作れるので、
それを`include`する。

モジュールの定義を参照するため、内部にサブモジュール`T`を作って、
- 直接include
- `Comparator.Make()`ファンクターに適用して戻ってきたモジュールをinclude
する。

```ml
let some_programming_books =
  Set.of_list
    (module Book)
    [ {title= "Real World OCaml"; isbn= "978-1449323912"}
    ; { title= "Structure and Interpretation of Computer Programs"
      ; isbn= "978-0262510875" }
    ; {title= "The C Programming Language"; isbn= "978-0131101630"} ]
```
`Set.of_list`でも、キーにするモジュールをFCMで受け取るので、そこでも使える。


# Base. ( Comparator.Make vs Comparable.Make )

Comparator.Make() は
  - `comparator_witness`
  - `comparator: (t, comparator_witness) Comparator.t`
を生成する。

Comparable.Make() は
  - 上記2点
  - + 色々
    - 比較演算子 : =, <>, <=, >=, ...
    - その他ヘルパー関数


つまり、機能的には
`Comparator.Make <<< Comparable.Make`

多くの場合はComparable.Makeでいい
(大は小を兼ねる)


# なぜ comparator_witness が必要？

witness = 立会人、証人
identify = 識別する

`comparator_witness`という謎の型の目的は、
使用されている比較関数を識別すること。

Map, Set では、異なるインスタンスが同じ比較関数を使用していることに
依存することがあるので、重要らしい。

例えば、`(String * Int)`のMapでも、同じSignatureで内部実装の比較関数が違うMapが存在する場合、
`comparator_witness`の型が異なるので型エラーになる。

## Map.symmetric_diff
２つのMapを差分計算する関数

比較する2つの`Map`モジュールが同じを持っている必要がある。

```ml
let left = Map.of_alist_exn (module String) [("foo", 1); ("bar", 3); ("snoo", 0)]
let right = Map.of_alist_exn (module String) [("foo", 0); ("snoo", 0)]

;;
Map.symmetric_diff ~data_equal:Int.equal left right |> Sequence.to_list

(* - : (string, int) Map.Symmetric_diff_element.t list =
[("bar", `Left 3); ("foo", `Unequal (1, 0))] *)
```


# The Polymorphic Comparator

```ml
Map.Poly.of_alist_exn digit_alist;;
- : (int, string) Map.Poly.t = <abstr>
```

コレを使うと、好きなタイプの連想リストからMapを作れる。


# 多相比較の危険性
基本的な方に対しては上手く機能するが、たまによきせぬ結果を生む

Map.equal関数
```ml
let m1 = Map.of_alist_exn (module Int) [1, "one";2, "two"];;
let m2 = Map.of_alist_exn (module Int) [2, "two";1, "one"];;

Map.equal String.equal m1 m2;;
```

Poly.(=) 演算子
```ml
Poly.(m1 = m2);;
(* Exception: (Invalid_argument "compare: functional value") *)
```

`Map.comparator`には、関数値が入っているのでエラーになる。

```ml
Poly.((Map.Using_comparator.to_tree m1) =
(Map.Using_comparator.to_tree m2))
;;
```

2つのマップの要素は、違う順番で追加されているので
内容比較は正しいが、構造比較は異なる結果でfalseになる。

Polymorph比較は、期待道理ではない結果を引き起こすことがあるので注意が必要
(とくにMapなどの複雑な型)


# [@@deriving]で Comparator.S を満たす

```ml
module Book = struct
  module T = struct
    type t = {title: string; isbn: string} [@@deriving compare, sexp_of]
  end

  include T
  include Comparator.Make (T)
end
```

- compare
- sexp_of_t
を手動で実装するのは面倒なので、ppxを使うことで簡単に実装できる。

間違えると嫌なので、タスクは自動化しよう
(base, ppx_jane  が必要)


# = vs ==, & phys_equal

OCamlにおいては
- `=`
  - 構造的等価性
  - 値比較
  - データ構造を再帰的にチェックして、個別のフィールド同士が同じかどうか
    - `<cyclic>`データ型が含まれていると、比較が無限ループになってハングアウトする！
- `==`
  - 物理的な等価性
  - ポインタ比較
  - 2つのデータ構造が、メモリ上で同じポインタに格納されているときに一致する

```ml
(* {mutable contents = 1} *)
let a = ref 1
let b = ref 1

a = b;; (* true *)
a == b (* false *)

!a = !b (* true *)
!a == !b (* true *)
```

## Core_kernelでの ==
`=` と `==` は
比較演算子として簡単に混在してしまうため、
Core_kernelでは`==` は Deprecated として警告される。

代わりに、`phys_equal`関数を使うと、`==`のようにアドレス比較しつつ警告が消える。


## = によるハングアップ
`=`は再帰的に構造を比較する。
下のように循環参照するものを比較すると簡単にハングアップする。


```ml
type t1 = { foo1:int; bar1:t2 } and t2 = { foo2:int; bar2:t1 } ;;;;
let rec v1 = { foo1=1; bar1=v2 } and v2 = { foo2=2; bar2=v1 } ;;;;

v1 = v1 ;;;;
```

# Map や Set に [@@ deriving sexp]する。

```ml
type string_int_map =
  (string,int,String.comparator_witness) Map.t
[@@deriving sexp]
```

そのままだとできない

```ml
type string_int_map =
  int Map.M(String).t
[@@deriving sexp]
;;
```
ファンクターで作成して、戻り値の`sig`に適用するとできる

# Trees

```ml
let ord_tree = Map.Using_comparator.to_tree ord_map;;
```

`Map.Using_comparator.to_tree`関数を使うと、
comparatorを含まない、マップのデータ構造を取得できる。

phantom type としての comparatorが、型に暗黙的に含まれる。

```ml
Map.Using_comparator.Tree.find ~comparator:Reverse.comparator ord_tree "snoo";;
```

違うタイプのcomparatorを使おうとするとエラー
`Map.Using_comparator.Tree.find`関数は、値を保存したときのComparatorを使って検索するという決まりがあるため



# Hash Table

Mapのいとこ

Mapと違う点
- mutable
  - 値を追加した新しいインスタンスを返すのではなく、内部の状態が更新される
- better time-complexity
  - 検索や追加の時間がMapよりも早い
    - Map : logarithmic, 対数的
    - HashTable : constant-time , 一定時間
- has hash_function
  - Map は 順序付きbinary treeを作成するための `compare` 関数を持つ
  - HashTable は、キーを整数値に変換する `hash` 関数を持つ


logarithmic = ロガリズミック、対数的な
# HashTable の時間複雑性
HashTableが一定時間アクセスを提供するには、いくつかの複雑さが隠されている

- テーブルがいっぱいになったらリサイズする必要
  - サイズが決まっている
  - サイズを超えそうになったら
    - より大きい格納テーブルを新しくメモリに確保
    - すべての内容を新しいテーブルにコピー
      - 平均的には一定速度だが、リサイズはコストが高い処理



- ハッシュの重複
  - キーをハッシュ関数で整数値にする
    - そのハッシュ値がかぶると同じバケットに格納されて、そのバケットでの検索が遅くなる
  - 質の悪い(かぶりやすい)ハッシュ関数を使うと、工場的なエラーを得られない。

Baseのハッシュテーブルは、バケットの部分がBinary Treeなので対数的なアクセス時間なので、
バケットにリストを使うよりは検索時間はまし。


## denial-of-service attack

サービス拒否攻撃
クエリを受けるサービスに対し、
身長に選んだハッシュ値のかぶるキーを大量に送って
サーバーのCPU負荷を高め、サービスを応答不能にする

DDoSの略語の元ネタ

BaseのHashTableは、バケット部分がBinaryTreeなので、
多少は耐性が高い




## Base.Hashtblのキー

`Core.Hashtbl_intf.Key`sig を満たすモジュールである必要がある。
```ml
#show Core.Hashtbl_intf.Key;;
module type Key =
  sig
    type t
    val t_of_sexp : Sexp.t -> t
    val compare : t -> t -> int
    val sexp_of_t : t -> Sexp.t
    val hash : t -> int
  end
```

Mapに加えて、`hash`関数を追加する代わりに
`comparator_witness`が必要ない。
(複数のHashTblで異なる`compare`関数を共有することがないため)

`[@@driving]`を使うとさらに楽

```ml
module Book = struct
  type t = { title: string; isbn: string }
  [@@deriving compare, sexp_of, hash]
end
;;
```

`Hashtbl.Poly.create`でも作れる
弱い多相性なので、最初に型のヒントが出た時点で固定される

```ml
let table = Hashtbl.Poly.create ();;
```

便利だが、基本的に`Poly`は予想外の動作を起こす可能性があるので
正確性が必要な場合は使わない

また、Polymorphic Hashtableは
リストなどの長い要素をハッシュ化しようとすると
最初の10要素だけしかハッシュしないので
ハッシュかぶりが大量に発生する可能性がある。


ppxで`[%hash: int list]`を使うと、ちゃんとハッシュする
もしくは、独自のハッシュ関数つかう
```ml
[%hash: int list] (List.range 0 9);;
- : int = 999007935
[%hash: int list] (List.range 0 10);;
- : int = 195154657
[%hash: int list] (List.range 0 11);;
- : int = 527899773
[%hash: int list] (List.range 0 100);;
- : int = 594983280
```

`ppx_hash`に含まれる


# Map vs HashTable

どっちを使うべきか

Map
- immutable
- 関数型プログラミング
- OCamlでは一般的にデフォルト

HashTable
- mutable
- 命令形プログラミング
- データ量が増えるほど、パフォーマンスはこちらの方が優れる。


パフォーマンスの質問では、ベンチマークを実行するのが最良

```ml
open Base
open Core_bench

let map_iter ~num_keys ~iterations =
  let rec loop i map =
    if i <= 0 then ()
    else loop (i - 1)
           (Map.change map (i % num_keys) ~f:(fun current ->
              Some (1 + Option.value ~default:0 current)))
  in
  loop iterations (Map.empty (module Int))

let table_iter ~num_keys ~iterations =
  let table = Hashtbl.create (module Int) ~size:num_keys in
  let rec loop i =
    if i <= 0 then ()
    else (
      Hashtbl.change table (i % num_keys) ~f:(fun current ->
        Some (1 + Option.value ~default:0 current));
      loop (i - 1)
    )
  in
  loop iterations

let tests ~num_keys ~iterations =
  let test name f = Bench.Test.create f ~name in
  [ test "table" (fun () -> table_iter ~num_keys ~iterations)
  ; test "map"   (fun () -> map_iter   ~num_keys ~iterations)
  ]

let () =
  tests ~num_keys:1000 ~iterations:100_000
  |> Bench.make_command
  |> Core.Command.run
```

```lisp
(executable
  (name      map_vs_hash)
  (libraries base core_bench))
```

```sh
dune build map_vs_hash.exe
./_build/default/map_vs_hash.exe -ascii -quota 1 -clear-columns time speedup
Estimated testing time 2s (2 benchmarks x 1s). Change using -quota SECS.

  Name    Time/Run   Speedup
 ------- ---------- ---------
  table    13.34ms      1.00
  map      44.54ms      3.34
```

tableの方が4倍くらい早い

ただし、必ずしもHashtblが最良というわけではない。
例えば、Mapの新旧バージョンを保持する必要がある場合、
元の値を破壊しない関数型プログラミングのほうがうまくいく場合もある。


```ml
(* 省略 *)
```

```sh
dune build map_vs_hash2.exe
./_build/default/map_vs_hash2.exe -ascii -clear-columns time speedup
Estimated testing time 20s (2 benchmarks x 10s). Change using -quota SECS.

  Name      Time/Run   Speedup
 ------- ------------ ---------
  table   4_453.95us     25.80
  map       172.61us      1.00
```



