# First-Class Module

OCamlには2つの言語部分がある

- 値、型
- モジュール、シグネチャ

モジュール + シグネチャの部分に、値と型を含めることはできる
逆はできない。


この階層化を避ける仕組みが First-Class Module

言語的には高度な部分

## Working with First-Class Module

`(module <Module_instance> : <Signature>)`


この構文で、モジュールを値として使える

```ml
open Base

module type X_int = sig
  val x : int
end

module Three : X_int = struct let x = 3 end

let three = (module Three : X_int)
```
`three`は、`(module X_int)`型の変数になる


型推論できるときだけ、`Signature`がいらない
(module ...)だけで書ける


```ml

let three = (module Three : X_int)
module Four = struct let x = 4 end
let numbers = [three; (module Four)]
```

## 匿名モジュール
コレも型推論できるときだけ許される
```ml
let numbers = [three; (module Four); (module struct let x = 5 end)]
```



## Unpack

First-Class Module 状態のモジュールの中身にアクセスするには、
通常のモジュールにアンパックする必要がある
`val`キーワードでできる

`(val <first-class module> : type)`

```ml
module Unpaked = (val three : X_int)
let num = Unpaked.x
```

## let module ... in

```ml
let to_int m =
  let module M = (val three : X_int) in
  M.x
```

`let ... in`式の中だけで
消費できるモジュールのアンパックもある


### plus modules
モジュールを足し算してみる例(to_intを使う)

moduleの中身を足し合わせて、First-Class Module で返す
```ml
let plus m1 m2 = (module struct let x = to_int m1 + to_int m2 end : X_int)

let six = plus three three
```

# Syntax shortcut

First-Class Moduleを

## 関数パターンマッチ
関数の引数に First-Class Module 構文
関数パターンマッチのような感じで、中身を取り出せる

```ml
let x (module M: X_int) = 
  M.x
```

## let module ... in パターンマッチ
普通のletではできない
moduleを格納するには`module`キーワードで定義しなければならないから


# Bumpable

```ml
module type Bumpable = sig
  type t

  val bump : t -> t
end

module Int_bumper = struct
  type t = int

  let bump t = t + 1
end

module Float_bumper = struct
  type t = float

  let bump t = t +. 1.
end

let int_bumper = (module Int_bumper : Bumpable)
let float_bumper = (module Float_bumper : Bumpable)

(* 計算 *)
let a =
  let (module Bumper) = int_bumper in
  Bumper.bump 1  
  (* error *)

(* 
Error: This expression has type int but an expression was expected of type
    Bumper.t
*)
```

上のように、異なるタイプの Int_Bumper と Float_Bumper
をBampable型のFirst-Class Module にした。

しかし、`Bampable.t`が抽象型なので、
このFirst-Class Moduleは対して計算とかできない。

First-Class Module にするときに、`Sharing constraint`すると、
型情報が明らかになるので計算を実行できる。

```ml
let int_bumper = (module Int_bumper : Bumpable with type t = int)
let float_bumper = (module Float_bumper : Bumpable with type t = float)
```

しかし、あんまり多層的ではない

# ローカル抽象型

コレまで使ってこなかった機能
`let`宣言するときに、`(type a)`のように書くことで
型変数を置き換えられる。

```ml

let int_bumper = (module Int_bumper : Bumpable with type t = int)
let float_bumper = (module Float_bumper : Bumpable with type t = float)

let bump (type a) (module Bumper : Bumpable with type t = a) (x : a) =
  Bumper.bump x

let bump_list (type a) (module Bumper : Bumpable with type t = a) (l : a list) =
  List.map ~f:Bumper.bump l
```

First-Class Module の作成側は
`with type t = int`とかでちゃんと具体化型にしないとエラーになる。

ローカル抽象型を利用する関数側でも、
関数の方のパターンマッチで`with type t = a`を書かないと計算できない



Signature定義、汎用的なModule定義に使うのは
グローバル抽象型ってこと？

## ローカル抽象型 : 詳細

- ' がいらない
  - 型シグネチャには自動でつく
- ローカルでは抽象型だが、外部から利用するときはpolymorphic


```ml
let wrap_in_list (type a) (x:a) = [x];;
```

外部から見ると多相型なので、リストにして返しても問題ない

```ml
let double_int (type a) (x:a) = x + x;;
(* error *)
```
内部的には抽象型なので、足し算しようとする(a = int とみなそうとする)
とエラー

## ローカル抽象型 : 用途


- ファーストクラスモジュールを引数にする関数で、FCMの内部の抽象型を置き換えるのに使う
- 普通の関数をファンクターっぽくする
  - First-Class Module を作って返す

```ml
module type Comparable = sig
  type t
  val compare : t -> t -> int
end
;;

let create_comparable (type a) compare =
  ( module struct
    type t = a

    let compare = compare end : Comparable
    with type t = a )
```
コレはただの関数だが、
ローカル抽象型を使って、モジュール内の具象型(具体的な型)を置き換えている。

引数で渡した`compare`関数の型から型推論して、モジュールを自動的につくる


## モジュールと値の結合

`Query_handler_instance`には`this`変数があり、
FCMとして作られるときに内部状態がインスタンス化される。

モジュールに固有の状態値が入っているのは
すごくオブジェクト指向に似ている

一つ違う点として、モジュールはオブジェクト指向のクラスより
たくさんのものをパッケージ化できる

- type
- module (module in module)
- 関数
- 変数




# その他の使い方
Fanctorにわたすローカルモジュールを即興で作るとか


# 例. Query Handler

S式のクエリを受けて、それを処理する機械を作る

## Interface

```ml
module type Query_handler = sig
  type config

  val sexp_of_config : config -> Sexp.t
  val config_of_sexp : Sexp.t -> config

  (* クエリハンドルサービス名 *)
  val name : string

  (* クエリハンドラの内部状態のデータ構造 *)
  type t
  (* ハンドラのインスタンスを作る *)
  val create : config -> t
  (* クエリを受け取って、評価を実行してレスポンスを返す *)
  (* Ok or Error *)
  val eval : t -> Sexp.t -> Sexp.t Or_error.t
end
```




## Implementation
実装は、色々


## Unique id Query Handler
今回はユニークid生成器を作る

```ml
module Unique = struct
  type config = int [@@deriving sexp]
  type t = {mutable next_id: int}

  let name = "unique"
  let create start_at = {next_id= start_at}

  let eval t sexp =
    match Or_error.try_with (fun () -> unit_of_sexp) with
    | Error _ as err -> err
    | Ok () ->
        let response = Ok (Int.sexp_of_t t.next_id) in
        t.next_id <- t.next_id + 1 ;
        response
end
```
現時点では、`eval`のsexpは使ってないので
何を入れてもユニークidが1更新されて出てくる

`Ok(Sexp.t ()) -> Ok(Sexp.t (t.next_id))`


```ml
let unique = Unique.create 0;;
(* val unique : Unique.t = {Unique.next_id = 0} *)
Unique.eval unique (Sexp.List []);;
(* - : (Sexp.t, Error.t) result = Ok 0 *)
Unique.eval unique (Sexp.List []);;
(* - : (Sexp.t, Error.t) result = Ok 1 *)
```

## Directory Listing Query Handler

lsコマンドの実装みたいなもの

```ml

module List_dir = struct
  type config = string [@@deriving sexp]
  type t = {cwd: string}

  let is_abs p = String.length p > 0 && Char.( = ) p.[0] '/'
  let name = "ls"
  let create cwd = {cwd}

  let eval t sexp =
    match Or_error.try_with (fun () -> string_of_sexp sexp) with
    | Error _ as err -> err
    | Ok dir ->
        let dir = if is_abs dir then dir else Core.Filename.concat t.cwd dir in
        Ok (Array.sexp_of_t String.sexp_of_t (Core.Sys.readdir dir))
end
```

`is_abs`が重要
S式クエリの文字列が
長さが1以上かつ、最初の一文字が`/`のときは絶対パスとみなし、
挙動を変える。

```ml
let list_dir = List_dir.create "/var";;
List_dir.eval list_dir (sexp_of_string ".");;
(* 
Ok
 (yp networkd install empty ma mail spool jabberd vm msgs audit root lib db
  at log folders netboot run rpc tmp backups agentx rwho)
*)
```


## 任意のクエリハンドラにクエリをディスパッチ

`Query_handler_instance` シグネチャを作る。
このモジュールはモジュール本体と
それに含まれる`t`のインスタンスを指す`this`変数だけをもつ

```ml
module type Query_handler_instance = sig
  module Query_handler : Query_handler

  val this : Query_handler.t
end


let build_instance (type a) (module Q : Query_handler with type config = a)
    config =
  ( module struct
    module Query_handler = Q

    let this = Q.create config end : Query_handler_instance )
```


# (module <Module>)
最初にあったように、`First-Class Module`を作る構文は、
型推論が効くときは`sig`を省略できる。

関数の引数に渡すときなど、`(module Int)`のように使ったりできる。


```ml
let unique_instance = build_instance (module Unique) 0
let list_dir_instance = build_instance (module List_dir) "/Users/masaharuhosomichi"
```


## 個別にクエリをディスパッチするシステムを作る

クエリの形式は、文字列のS式とする

`(query-name query)`

query-nameは、クエリを送る先のサービス(Query_Handler)を示す
queryは内容

### build_dispatch_table 関数

サービス名をキー、ハンドラーのインスタンスを値とした
`dispatch_table`をハッシュテーブルとして作る。
```ml
let build_dispatch_table handlers = 
  let table = Hashtbl.create (module String) in 
  List.iter handlers 
    ~f:(fun ((module I : Query_handler_instance) as instance) -> Hashtbl.set table ~key:I.Query_handler.name ~data:instance);
    table
```


### dispatch 関数

`dispatch_table` と `name_and_query`をとり
クエリハンドラーにクエリを送る

```ml

let dispatch dispatch_table name_and_query =
  match name_and_query with
  | Sexp.List [Sexp.Atom name; query] -> (
    match Hashtbl.find dispatch_table name with
    | None -> Or_error.error_string "Could not find matching handler"
    | Some (module I : Query_handler_instance) ->
        I.Query_handler.eval I.this query )
  | _ -> Or_error.error_string "malformed query"

```
パターンマッチが大活躍なコード

最初に、`Sexp.List`でマッチング
`Sexp.Atom`が1番目にあり、queryのS式が2番目にある(つまり2要素リストにマッチする)

更に、`(module ... : sig)` 構文で、ハッシュテーブルから取り出してきた
First-Class Module をマッチングしつつ取り出す。


終了するときは`quit`
`Sexp.Atom` には()がつかないのに注意

Sexp.Atom "quit" = `quit`
Sexp.List [Sexp.Atom "quit"] = `(quit)`




# クエリハンドラの Load / Unload

First-Class Module のメリットの一つ
強力な dynamizm と flexybility

つまり、動的で柔軟な設計にできる

実行中にモジュールを読み込んだり、外したりできる。

## Loader Query Handler

Queryハンドラーの制御クエリの処理をするハンドラーを作る

### データ構造

```ml
module Loader = struct
  open Core

  type config = (module Query_handler) list sexp_opaque [@@deriving sexp]

  type t =
    { known: (module Query_handler) String.Table.t
    ; active: (module Query_handler_instance) String.Table.t }

  let name = "loader"

  ...
end
```
`config`型は、インスタンスを作成するときに必要な情報
ハンドラーモジュールのファーストクラス list を受け取って、文字列テーブルに保持する感じ？

`String.Table.t`を使うには`Core`をopenする必要があるのでしておく
(Hashtblの文字列版っぽい。dataの部分だけ多相的)

- known : ロードできる種類のテーブル
- active : ロードされているインスタンスのテーブル


### 関数
QueryHandlerのインターフェースに沿って作る


create
```ml
  let create known_list =
    let active = String.Table.create () in
    let known = String.Table.create () in
    List.iter known_list ~f:(fun ((module Q : Query_handler) as m) ->
        Hashtbl.set known ~key:Q.name ~data:m) ;
    {active; known}
```

know_list は First-Class Module List
内部状態の`known`のテーブルに登録して、データ構造を返す


load / unload
```ml
  let load t handler_name config =
    if Hashtbl.mem t.active handler_name then
      Or_error.error "Can't re-register an active handler" handler_name
        String.sexp_of_t
    else
      match Hashtbl.find t.known handler_name with
      | None -> Or_error.error "Unknown handler" handler_name String.sexp_of_t
      | Some (module Q : Query_handler) ->
          let instance =
            ( module struct
              module Query_handler = Q

              let this = Q.create (Q.config_of_sexp config) end
            : Query_handler_instance ) in
          Hashtbl.set t.active ~key:handler_name ~data:instance ;
          Ok Sexp.unit

  let unload t handler_name =
    if not (Hashtbl.mem t.active handler_name) then
      Or_error.error "Handler not active" handler_name String.sexp_of_t
    else if String.( = ) handler_name name then
      Or_error.error_string "It's unwise to unload yourself"
    else (
      Hashtbl.remove t.active handler_name ;
      Ok Sexp.unit )
```

eval で実行するものその1
クエリハンドラー名 + configを受け取って、
インスタンスを作って
activeテーブル に ロードする

既にロードしてあったらエラーを返して終わり

unloadはその逆



### eval

eval関数を実装する前に、
`Loader`ハンドラーが受け取る可能性のあるクエリの種類を、
`request`タイプとして定義し、S式への変換を自動実装する


```ml

  type request =
    | Load of string * Sexp.t
    | Unload of string
    | Known_services
    | Active_services
  [@@deriving sexp]

  let eval t sexp =
    match Or_error.try_with (fun () -> request_of_sexp sexp) with
    | Error _ as err -> err
    | Ok resp -> (
      match resp with
      | Load (name, config) -> load t name config
      | Unload name -> unload t name
      | Known_services -> Ok ([%sexp_of: string list] (Hashtbl.keys t.known))
      | Active_services -> Ok ([%sexp_of: string list] (Hashtbl.keys t.active))
      )

```
type定義に従って書けばいいので、
タイプセーフで簡単にeval関数を実装できる。

S式への変換の実装は ppx で自動的に行われる



#### Hashtbl.mem
`Hashtbl.mem ; t -> 'a0 -> bool`
'a0 のエントリがテーブル内にあるかどうか

あまり細かいことは気にせず、キーが存在するかどうかで理解することにした。


## `[@sexp.opaque]`

特定の型を、`sexp`に変換できないものとしてマーク(注釈)するppxディレクティブ
コンパイラへの支持

t -> sexp へはできるが、
sexp -> t をやろうとするとエラー

`to_sexp`関数では atom `<opaque>`として表示される
シリアライズできないものを見せかけだけシリアライズする感じ？

```ml
(* 非推奨、コンパイルできない *)
type config = (module Query_handler) list sexp_opaque
  [@@deriving sexp]

(* ppx, 推奨 *)
type config = ((module Query_handler) list[@sexp.opaque]) [@@deriving sexp]
```

## dune ファイル

```
(executable
 (name main)
 (libraries core core_kernel ppx_sexp_conv)
 (preprocess
  (pps ppx_sexp_conv)))
```

`ppx_jane`を使わず、
最低限の`ppx_sexp_conv`プリプロセッサだけロードする


## 試してみる

S式では、大文字と小文字の区別がないらしい
```ml
  type request =
    | Load of string * Sexp.t
    | Unload of string
    | Known_services
    | Active_services
  [@@deriving sexp]
```
`request`タイプはS式に変換されるので
バリアント部分の文字列指定は小文字でも良い



```sh
# 引数なしバリアントは Sexp.Atom string になるっぽい
>>>(loader known_services)
# (unique ls)

# 大文字でも良い
>>>(loader Known_services)

>>> (loader active_services)
# (loader)

# 引数付きバリアントは、Sexp.Listっぽい
>>> (loader (load ls .))
()
>>> (ls _build)
(default .digest-db .to-delete-in-source-tree log .db .filesystem-clock
 .aliases)


# 同じく、Unload バリアントも Sexp.List だった
>>> (loader (unload ls))
()
>>> (ls .)
("Could not find matching handler" ls)
```

## ポイント

基本的に、`cli`関数の仕様はそのまま

- 最初から `loader`ハンドラだけロードされている
- `loader`をunloadすることはできない
- `loader.known`にloader自身は含まれないので、loaderをロードすることはできない
  - loadもunloadもできない
  
## さらに
OCamlのDynamic Linking機能を使うと、
更にダイナミズムを活かすことができる。
OPAM経由でライブラリをインストールして、
プラグインを適用とかできる。　

要するに、プラグインを使うソフトウェアの実装は、
First-Class Module を使うと便利ということ



## Living without First-Class Module

First-Class Module を使ってできるほとんどのデザインは、
不自然さや面倒さを伴いつつも、それを使わず実現できる


```ml
type query_handler_instance = 
  { name : string;
    eval : Sexp.t -> Sexp.t Or_error.t
  }

type query_handler = Sexp.t -> query_handler_instance;;

let unique_handler config_sexp =
  let config = Unique.config_of_sexp config_sexp in
  let unique = Unique.create config in
  { name = Unique.name
  ; eval = (fun config -> Unique.eval unique config)
  }
;;
```

`query_handler_instance` Record は
内部に`eval`関数を持っている。

`unique_handler`のインスタンスを作るときには、
その関数の中になにか値や設定がバインドされて返される。

つまり一種のクロージャーを使った設計
(関数を返す関数、自由変数を関数の中にバインドしてその関数を返す)


コレくらいの規模のモジュールだと、
First-Class Module を使うほどでも無く実現できるが、
クロージャー(関数を返す関数)に隠れた機能が増えれば増えるほど、
アプローチは厄介になる。



