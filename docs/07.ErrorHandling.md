[TOC]

# Error Handling 
エラー処理が好きな人はいない。
しかし、エラー処理は重要。


OCamlには、最小限の労力で確実にエラーを処理する強力なツールが有る。


# Error-Aware Return Type
OCamlでエラーを通知する最良の方法は、戻り値にエラーを含めること


- `'a option`
- `'a 'b Result.t`


# compute_bounds
bounds = 境界値
リストの最小値と最大値を見つけ、両方あるときは `Some(x, y)`, どちらか無いときは`None`を返す


```ml
let compute_bounds ~compare list =
  let sorted = List.sort ~compare list in
  match (List.hd sorted, List.last sorted) with
  | None, _ | _, None -> None
  | Some x, Some y -> Some (x, y)
```
Noneはそのまま返し、
エラーの対処は呼び出し元が行う。


# Encoding Error with Result


option はエラーを報告するために十分な表現力ではない場合もある。
`'a 'b Result.t`はエラーに情報をもたせる。

```ml
module Result : sig
  type ('a, 'b) t = 
    | Ok of 'a 
    | Error of 'b
```

Variant型
`Ok`と`Error`バリアントはトップレベルで使える。
タプル型で型パラメータが指定されてる。

```ml
[ Ok 3; Error "abject failure"; Ok 4 ];;
```

`Ok` と `Error` コンストラクタは、
何故か`Base`や`Result.t`モジュールを開かなくても使えるっぽい


# Error & Or_error

Resultタイプの Error 'b にはいろいろなタイプをもたせられるが、
一番いいのは 標準の Error タイプを作って返すこと。

しかしどのようなタイプにすれば良いのか(文字列？XML？)
Base ではこの答えとして `Error.t` タイプを提供している。

エラー文字列などは、生成に時間がかかるkが
`Error.t`では遅延評価を使うことで、必要になるまでエラー文字列の生成を後回しにする。


## 遅延評価 vs 正格評価

`Error.of_string`は、その場で評価されてエラーメッセージを持つ`Error.t`になる。
```ml
Error.of_string "something went wrong";;
```

`Error.of_thunk`は、`unit -> string` 関数をエラーの値として受け取る。
こちらは、文字列として変換されるまで評価されないので
遅延の恩恵を受けられる。
```ml
Error.of_thunk (fun () ->
Printf.sprintf "something went wrong: %f" 32.3343)
```


# Error.t と S式
Error.t を作るための一般的な方法は
S-expression を使うことだとされている。

s式 = 丸括弧で囲んだコンスリスト
```lisp
(This (is an) (s expression))
```

`Error.create`関数を使うと、S式の形式で`Error.t`を作成する
```ml
Error.create "Unexpected number" 1 Int.sexp_of_t ;;
Error.create "Unexpected character" 'z' Char.sexp_of_t ;;
```

## Type.sexp_of_t val
ある値を元にして S式を作る関数
Char でも Int でも String でも使える

S式を使う理由は、後にデータをシリアル化するのに使えるから
(他のプログラムやデータの受け渡しで使いやすくするため?)
18章でやるらしい。

# [%sexp_of: type]
自分で自由に好きなタイプのS式を作る関数式を返す。
基本、タプル型にすると良さそう `*`

`Base`に含まれる`<Type>.sexp_of_t`に依存してるので
openしておく。

```ml
open Base;;
#require "ppx_jane";;
let custom_to_sexp = [%sexp_of: float * string list * int]
(* val custom_to_sexp : float * string list * int -> Sexp.t = <fun> *)
```

let定義したエラーS式生成関数に、引数を適用すると
S式が戻る。 

```ml
custom_to_sexp (3.5, ["a";"b";"c"], 6034);;
(* Sexp.t = (3.5 (a b c) 6034 *)
```

## イディオムの中で ppx を使う

`[%sexp_of: タプルの型]`は関数を返すので、
`Base.Error.create`の第3引数にそのまま使える。

```ml
let e =
  Error.create "Something went terribly wrong"
    (3.5, [ "a"; "b"; "c" ], 6034)
    [%sexp_of: float * string list * int]
```

%のつくppxは、普通に式みたいな感じで使えるイメージ

```ml
[%sexp_of: float * string list * int] (3.5, [ "a"; "b"; "c" ], 6034);;
(* - : Sexp.t = (3.5 (a b c) 6034) *)
```

### dune ファイルでプリプロセッサの設定
ppxを使ったコードをコンパイルするには、`(preprocess ...)`の設定が必要
[General concepts — dune documentation](https://dune.readthedocs.io/en/stable/concepts.html#preprocessing-specification)


# Error.of_list
`'a list`型から`Error.t`を作る

```ml
Error.of_list [ Error.of_string "Your tires were slashed";
                   Error.of_string "Your windshield was smashed" ];;
// - : Error.t = ("Your tires were slashed" "Your windshield was smashed")
```

リストになった文字列エラーの`Error.t`
S式っぽくなら

# Error.tag
エラーに文字列でタグ付けする。
`(string 'a)`のS式っぽい`Error.t`型になる。

```ml
Error.tag
  (Error.of_list [ Error.of_string "Your tires were slashed";
                   Error.of_string "Your windshield was smashed" ])
  "over the weekend"
;;
// - : Error.t =
// ("over the weekend" "Your tires were slashed" "Your windshield was smashed")
```


# Or_error

`'a Or_Error` = `'a Error.t Result`

Baseが提供する`Error.t`型をエラーの型にした
`Result.t`の省略形


# bind関数 : >>=

`Base.Option.bind`で既に定義されているやつ

`>>=`演算子の関数
モナドのやつ。

```ml
let bind option f =
  match option with 
  | None -> None 
  | Some x -> f x
;;

let maybe_val = Some 'a';;ci
maybe_val >>= print_char;; (* () *)
let none_val = None;;
none_val >>= print_char;; (* None *);;
```

ここで重要なのは、
Noneを返すケースが有るため 型推論によりReturn type は `'a option`
なので、fは`f: 'a -> 'b option`でなければならない

option戻り値の関数にモナドをバインド
戻り値を更にoption戻り値の関数にバインド ...
エラーハンドリングは最後にやる。

というような、Railway でエラーハンドリングできる。

## Base.Option.Monad_infix
Monadの入力前置詞を定義するモジュールを
openしておくと、簡単に使える

```ml
open Option.Monad_infix

let a = None >>= compute_bounds ~compare:(fun a b -> a - b)
```
これはOption専用なので、Resultとかは他のモジュールをopenする必要がある。
`open Result.Monad_infix`とか


小さい例では、bindを使わないで普通に option をマッチさせたほうが早い。

多くの段階のエラー処理を伴う大規模で複雑なものに対しては
Bind Idiom が非常に有効
明確で管理しやすい

# Monad & Let_Syntax

`ppx_let`プリプロセッサが必要
duneファイルに書くか、utopで`#require`しておく


`let syntax`は
Haskellの`do`アクションの中で、
モナド値に依存する変数をバインドするときに使える。

`ppx_let`で解釈される`let%bind`はそれのOCaml版

すなわち、`>>=`でモナドを渡していくフローを
一時変数に代入するような感じに見せかける拡張構文

```ml
let compute_bounds ~compare list =
  let open Option.Let_syntax in 
  let sorted = List.sort ~compare list in
  let%bind first = List.hd sorted in 
  let%bind last = List.last sorted in 
  Some (first, last
```

変換は次のような感じ

```ml
let%bind x = some_expr in some_other_expr
;;
some_expr >>= fun x -> some_other_expr
```

`other_expr`の型もまたモナド
モナドの中身を引数にとり、モナドを返すラムダ式(関数)にバインドするのを
let in 式で引数に入ってくるモナドを変数に置き換えたような表現にする。

メリットは、`Monadic Binding`を通常のlet binding に見せかけることができる。
さらに、そのままの見た目に近い形でエラーハンドリングもできる。

# Option.both

```ml
Option.both;;
(* - : 'a option -> 'b option -> ('a * 'b) option = <fun> *)
```

Option値を2つとって、タプルのoption値に変換する。
compute_boundsを更に短くできる。

ある2つのMonad的関数の戻り値が両方成功じゃないと行けないケースの実装に使えそう

```ml
let compute_bounds ~compare list =
  let sorted = List.sort ~compare list in
  Option.both (List.hd sorted) (List.last sorted)
```

## Monadまとめ

Optionの例
- `>>=`
  - bind 関数
  - Option.Monad_infix
- ppx_jane
  - `[%sexp_of: (string * string* ...)]`
    - S式でエラーを作る
      - Result.tのエラー部分のペイロードにする
      - `Or_error.t`を使うと同じこと
      - シリアル化に便利
    - Error.create
    - Error.tag
    - Error.list
- ppx_let
  - `let%bind`
    - `let x = expr in other` = `expr >>= fun x -> other`
    - Monad Bindingを普通のlet binding っぽく見せる







# Exception

Javam C#, Python と同じ
try - catch 機構

Errror-Aware Return Type と違ってあんまり賢くないやり方なので
Monadになれたら使わない方が良い


```ml
3/0;;
Exception: Division_by_zero.
```

何も処理しないとプログラムが止まる。

```ml
List.map ~f:(fun x -> 100 / x) [1;3;0;4];;
Exception: Division_by_zero.
```

List.map のリストに含まれてたりすると気づきにくくて危険


# define Exeption

例外も、モジュールや型と同様に定義できる

```ml
exception Key_not_found of string;;
(* Error Constructor  *)
Key_not_found "hello"
(* - : exn = Key_not_found("hello") *)
```
`exn`型の値を定義
= 右辺 とかいらない

引数付きVariantとかと同じ様に、コンストラクターを使って作成

# raise
エラーを発火する関数

```ml
raise;;
(* - : exn -> 'a = <fun> *)
raise (Key_not_found "hello");;
(* Exception: Key_not_found("hello"). *)
```

コンストラクタは関数とみなされるっぽいので、
`()`で区切らないと引数多すぎと起こられる


# exn 型
特殊なケース
型システムの例外の一つ


# ****_exn という関数名
OCamlでは、慣習的に
内部で Exception を raise する関数は
`_exn`という後置詞をつけているっぽい


例
```ml
let rec find_exn alist key = match alist with
  | [] -> raise (Key_not_found key)
  | (key',data) :: tl -> if String.(=) key key' then data else find_exn tl key
;;
```

使うときに例外を投げると気づかないと、エラー処理できないため？
色々ある

-  List.find_exn
-  List.zip_exn


使うときは、リカバリーを仕込むか
絶対にエラーにならないように使う

# raise 関数の戻り値型

```ml
raise;;
- : exn -> 'a = <fun>
```

`exn` を引数にして
``a` を返す。

しかし、この戻り値を使うことはできない
例外を投げたらすぐ終了してしまうため。

このような、実際に使われない戻り値型が`'a`と置かれているパターンはよくある。
`unit`を明示的に返さないとこうなる。
こういう場合は戻り値をなにかに利用しようと思ってはいけない。

無限ループ再帰関数でも似たような現象が起こる

```ml
let rec forever () = forever ();;
val forever : unit -> 'a = <fun>
```

ただ、わざわざ`'a`で置かれるのは意味がある。
プログラムのどこにいても例外を投げられるように、`raise`の戻り値はどの値にもフィットするようになっている。
(特定の型だとコンパイル時に型エラーになる可能性がある)


TypeScriptとかだと `Never` が使われる状況


# `[@@deriving sexp]`
ppxを使って、Exceptionを定義するときにS式に変換してくれる関数を自動実装する。

```ml
type 'a bounds = { lower: 'a; upper: 'a } [@@deriving sexp];;
(* 
  type 'a bounds = { lower : 'a; upper : 'a; }
  val bounds_of_sexp : (Sexp.t -> 'a) -> Sexp.t -> 'a bounds = <fun>
  val sexp_of_bounds : ('a -> Sexp.t) -> 'a bounds -> Sexp.t = <fun> 
*)
exception Crossed_bounds of int bounds [@@deriving sexp];;

Crossed_bounds { lower=10; upper=0 };;
- : exn = (//toplevel//.Crossed_bounds ((lower 10) (upper 0)))
```

Sexplib ライブラリと ppx によって提供されるS式サポート
18章でデータのシリアル化をするとき役に立つ？



# Helper function for throw Exception

## failwith 
`Failure msg`例外を`raise`するヘルパー関数
とりあえずエラーメッセージを適当なExceptionで`raise`そのままする使うより手っ取り早い

```ml
let failwith msg = raise (Failure msg);;
```
## assert

`bool`値を使って例外を起動する。
関数というよりディレクティブ？
もともと組み込まれてるキーワードっぽい

```ml
let merge_lists xs ys ~f =
  let rec loop xs ys =
    match xs,ys with
    | [],[] -> []
    | x::xs, y::ys -> f x y :: loop xs ys
    | _ -> assert false
  in
  loop xs ys
;;
merge_lists [1;2;3] [-1] ~f:(+);;
(* Exception: "Assert_failure //toplevel//:6:14" *)
```
`false`を引数にすると、
`Assert_failure`例外が発生する

他の例外と比較して特殊な点として、ソースコード上の行番号、列番号が表示される。


# Exception Handler
ここまで、例外を定義するのと例外を`raise`することだけやってきたが、
実際は例外から回復したい。

# try-with statement

```ml
try <expr> with
| <pat1> -> expr
| <pat2> -> expr
...
```
パターンマッチで、例外にマッチしたらその式を評価して返す
例外が発生しなければ一番最初の式が評価されて返される。

関数呼び出しのスタックを、順々に上へとさかのぼってマッチされていく
例外がcatchされないときはプログラム終了

```ml
try
  try
    <expr>
  with
  | pat1 ->
  | pat2 ->
with
| pat3 ->
| pat4 ->
...
```




# Cleaning Up

`try - finaly` のように、例外を処理し終わったあと/例外がキャッチされずプログラムを終了する時
に何かしらの処理を行いたい場合

`Base.Exn.protect`関数を使うことで実現できる。
プリミティブな言語機能ではなくライブラリで実装されている。

```ml
open Stdio

let parse_line line =
  String.split_on_chars ~on:[ ',' ] line |> List.map ~f:Float.of_string

let load filename =
  let inc = In_channel.create filename in
  let data = In_channel.input_lines inc |> List.map ~f:parse_line in
  In_channel.close inc;
  data
```
`In_channel.close inc;`が実行される前に、例外で終了してしまうと
ファイルが閉じられない


```ml
let load filename =
  let inc = In_channel.create filename in
  Exn.protect
    ~f:(fun () -> In_channel.input_lines inc |> List.map ~f:parse_line)
    ~finally:(fun () -> In_channel.close inc)
```

`Exn.protect ~finaly:`に指定した関数が、例外が発生した場合でも実行されるので安心.
エラーが起きたら`In_channel.close`でファイルを閉じる

## In_channel.with_file
上のような問題では、わざわざ`Exn.protect`を使わなくても
`In_channel.with_file`関数を使えば
エラーが有ったときに自動でファイルを閉じつつ処理を実行できる。
(`Exn.protect`を利用して実装されているらしい)
```ml
let load filename =
  In_channel.with_file filename ~f:(fun inc ->
    In_channel.input_lines inc |> List.map ~f:parse_line)
```


# Catching Specific Exceptions
特定の例外をキャッチする
`try-with`でラップする処理の範囲は小さくしようという話


```ml
let lookup_weight ~compute_weight alist key =
  try
    let data = find_exn alist key in
    compute_weight data
  with
  Key_not_found _ -> 0.
;;
```
`find_exn`は、見つからなかったら`Key_not_found`例外をraiseする。
しかし、
- `compute_weight`でなにか例外が起こったら？
- その例外が`Key_not_found`で被っていたら？

型システムは関数が投げる例外を教えてくれないので
上記の問題を検知できない。
なるべく例外ハンドラの範囲を狭くして、コードのどの部分で失敗したかを
明確にするようにコーディングする。

```ml
let lookup_weight ~compute_weight alist key =
  match
    try Some (find_exn alist key)
    with _ -> None
  with
  | None -> 0.
  | Some data -> compute_weight data
;;
```
`try-with`
問題なかったら Some x
エラーが起きたら None 
を返す式にする。

上記は少々冗長
OCamlのmatch式は `exception`をマッチできるので短く書ける

```ml
let lookup_weight ~compute_weight alist key =
  match find_exn alist key with
  | exception _ -> 0.
  | data -> compute_weight data
;;
```

## exn関数の回避
自分で実装するときは option や Result.t を使って
なるべく `exception` に頼らない実装にする。

標準ライブラリにも、`exn`がつかない関数にすれば
optionで実装されていることが多いのでそっちを使う。

```ml
List.Assoc.find
List.Assoc.find_exn
```
どっちも使えるけど、上は option で実装されてる。



# Backtraces

例外の大きな価値は、スタックのバックトレースで有用なデバッグ情報を提供すること


```ml
open Base
open Stdio
exception Empty_list

let list_max = function
  | [] -> raise Empty_list
  | hd :: tl -> List.fold tl ~init:hd ~f:(Int.max)

let () =
  printf "%d\n" (list_max [1;2;3]);
  printf "%d\n" (list_max [])
```

```sh
dune exec -- ./blow_up.bc
3
Fatal error: exception Blow_up.Empty_list
Raised at file "blow_up.ml", line 6, characters 16-26
Called from file "blow_up.ml", line 11, characters 16-29
```

ソースコードの何行目の、何文字目で発生したかを教えてくれる


`Base`を使っているとデフォルトでバックトレースがオンになるらしい
環境変数でoffにできる

コード内で
`Backtrace.Exn.set_recording false`を呼び出すことでもoffにできる。

OCamlの例外は高速だが、
バックトレースを無効にすると更に高速になる。
最適化するときはエラーログを外すと良い


# From Exceptions to Error-Aware and Back Again

OCamlのプログラミングには
- Error-Aware type
- Exception
の両方が必要で、2つの世界を行き来することがよくある。

`Base`にはそのためのヘルパー関数がある。
- 例外の発生を `option` に取り込む
- その逆


## Option.try_with

```ml
Option.try_with;;
(* (unit -> 'a) -> 'a option *)
```

引数は、`unit -> 'a`の関数
何かしら例外が発生する可能性のある関数を実行して、
- 成功したら Some x
- 例外発生したら None
を返す。

```ml
let find alist key =
Option.try_with (fun () -> find_exn alist key)
```

`****_exn`のついた関数も、`Option.try_with`で option に変換できる。

`_exn`を取りのぞいた関数名にバインドすると良いかも

`Result.t`と`Or_error.t`にも同様の`try_with`関数がある。


# Choosing Error-Handling Strategy

適切なエラーハンドリング戦略の選択

OCamlは try-with と Error-Aware return の両方の
戦略をサポートしている。

## 簡潔さと明示性のトレードオフ

Exception は、エラー処理作業を大きなスコープに任せる。
メリット
- 型が乱雑にならない
- 戻り値がoptionなどにラップされずすぐ使える
デメリット
- コードが生成する可能性のあるエラーを`explicit`に無視できない
  - エラーの見逃しは暗黙的で、見過ごされる可能性がある。


Exceptionは、書いて捨てるようなコードで
失敗しても対してコストが掛からない(ユーザーの迷惑にならない、開発者の個人的なバッチ)
ラフなプログラム、スクリプトなら使ってもOK

> The maxim of “use exceptions for exceptional conditions” applies.
例外は例外的な条件で使用するという格言がある
そもそも例外が発生する頻度が十分に低いなら、例外を投げるのが正しい動作になることが多い。
完璧に例外を避けても意味がない場合もある。

また、どこにでも発生しうるエラーでは、戻り値の型は過剰かもしれない
例. out-of-memory error

すべての操作が`失敗する可能性がある`とマークすることは
マークしないことと変わりない。

## Error-Aware type の使い所

- 発生場所が予測できる
  - ある関数で発生する
- プログラムの一般的な部分



# まとめ

Exceptionを使う場所
- どこでも発生しうる
- 発生頻度が低い
  - 例. out-of-memory

Error-Aware typeを使う場所
- 発生場所が予測できる
- 一般的な部分
  - List.find で見つからない場合など

