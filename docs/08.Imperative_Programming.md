# Imperative Programming
Imperative = 命令的


これまで勉強したコードは `pure`

pure <> impure

`impure`なコードとは

- 内部状態を変異 (mutation)
- I/O の実行
- clock (time)
- 変更可能な部分と相互作用する

といったコードのこと。
side effect をもち、実行のたびに異なる結果を返す可能性がある。


## pure vs impure

OCamlでは pure がデフォルト

しかし、パフォーマンスを向上させるために命令的プログラミングは重要。
(命令的技術でのみ効率的に実装できるアルゴリズムが多くある)

よって、OCamlでは `pure` を簡単かつ自然にしつつ
`impure`な命令的プログラミングをサポートする


# 例. Imperative Dictionaries

ここでの実装はtoy
実際はBaseや標準ライブラリの辞書がもっと効率的な実装になってる

`('a 'b) t`

## interface

```ml
open Base

module type Dictionary = sig
  type ('a, 'b) t

  val create : hash:('a -> int) -> equal:('a -> 'a -> bool) -> ('a, 'b) t
  val length : ('a, 'b) t -> int
  val add : ('a, 'b) t -> key:'a -> data:'b -> unit
  val find : ('a, 'b) t -> f:(key:'a -> data:'b -> unit) -> unit
  val remove : ('a, 'b) t -> 'a -> unit
end
```

add, find, remove では `unit`を返しているが、
関数型のコードでは珍しいことである。

主な目的が値を計算することではなく、
データ構造を mutation(変異)させることなので
命令的関数ではよく見られる。


## imprement

順番に見ていく

## ('a, 'b)t
データ構造の本体
キー、バリューと
その周辺情報を格納する。

```ml
type ('a, 'b) t = { mutable length: int;
                    buckets: ('a * 'b) list array;
                    hash: 'a -> int;
                    equal: 'a -> 'a -> bool;
                  }
```

- length : 長さ。入っているキーの数(bucketsすべての長さの累計)
- buckets: (a*b)タプルリストの配列
  - 固定長配列の各セクションに、可変長のListが入っている。
  - ハッシュ(string->int)で導出されるインデックスにより、検索速度は`bucket`の逆数 * かぶり具合 で早くなる
  - `Array`の中身はデフォルトで `mutable`
    - というか ref
- hash: ハッシュ関数
  - Dictionaryを作るときに、使うアルゴリズムを変えられる。
- equal: 比較関数
  - キーの比較に使う
  - アルゴリズムにより、検索速度が変わったり
  - find, add(置き換え), remove など、内部のヘルパー関数から使ったりもする。



## create, length, find

どれにも共通することだが、
第一引数は HashTable のレコード(データ構造)を引数にする。

関数型におけるオブジェクト指向みたいなもん

```ml
module Dictionary : Dictionary = struct
  let num_buckets = 17
  let hash_bucket t key = t.hash key % num_buckets

  let create ~hash ~equal =
    {length= 0; buckets= Array.create ~len:num_buckets []; hash; equal}

  let length t = t.length

  let find t key =
    List.find_map
      t.buckets.(hash_bucket t key)
      ~f:(fun (key', data) -> if t.equal key' key then Some data else None)
```

moduleは以下のレベルはファイルと同じ
`.mli`に書いてないlet bindingも書ける(公開はされない)

- num_buckets
- hash_bucket
  - ハッシュは、bucketsの長さを決めておくもの
  - hash値を計算するユーティリティも実装しておくと便利
    - 大体は、ハッシュ値 % bucketsの長さ のあまり

- create
  - 普通に作る
  - ハッシュ関数と比較関数を渡す
  - Arrayの各セクションは`[]`で初期化する。

- length
  - add/remove で増減するカウントを記録しといてそれを返すだけ(副作用的なアプローチ)

- find
  - キーを渡して値を探す
  - あったら Some x, なかったら None
  - array.(i) : 配列のインデックスにアクセスする構文
    - 引数の key の hash値 % buckets.length の bucket でリストを走査する。
      - `List.find_map` : find と map の複合関数。見つけた値に関数を適用して返す。
        - option でラップするのに使う

##  iter

中身全部を、副作用関数に適用するだけ
戻り値はunit。

全部printするとか

```ml
let iter t ~f =
  for i = 0 to Array.length t.buckets - 1 do
    List.iter t.buckets.(i) ~f:(fun (key, data) -> f ~key ~data)
  done
```

for文を使っている。
`list Array.t`型はある意味2次元リスト
それを全部`for式`で操作するような仕組みになっている。

`Array.iter` 使っても良い気がするけど、
ここは自分で実装することにこだわったかんじか

## add, remove

この2つの関数は、他の関数と違って値を計算することではなく
引数で受け取ったデータ構造を変更することが目的。

- 要素の追加/削除
- lengthの増減

この2つの impure な操作をする。

```ml
  let bucket_has_key t i key =
    List.exists t.buckets.(i) ~f:(fun (key', _) -> t.equal key' key)

  let add t ~key ~data =
    let i = hash_bucket t key in
    let replace = bucket_has_key t i key in
    let filtered_bucket =
      if replace then
        List.filter t.buckets.(i) ~f:(fun (key', _) -> not (t.equal key' key))
      else t.buckets.(i) in
    t.buckets.(i) <- (key, data) :: filtered_bucket ;
    if not replace then t.length <- t.length + 1

  let remove t key =
    let i = hash_bucket t key in
    if bucket_has_key t i key then (
      let filtered_bucket =
        List.filter t.buckets.(i) ~f:(fun (key', _) -> not (t.equal key' key))
      in
      t.buckets.(i) <- filtered_bucket ;
      t.length <- t.length - 1 )
```

1. keyを受け取る
2. keyの hash値 % buckets.length を求める (= i)
3. i番目のbucketにアクセス
4. 値を探す
- あったら置き換え
- なかったら追加
  - Arrayのセクションは `mutable` なので、新しい値(data :: bucket)を作って参照に入れる`<-`
5. count を+-1する

削除の場合は、キーの中身を`List.filter`したものを`buckets.(i)`に割当する。

### bucket_has_key 関数
add / remove で使うヘルパー関数
キーが含まれているかどうかを知らべて挙動を変える

共通で使うのでモジュールトップに定義したが、
1関数でしか使わないなら let ... in でも良いかも


# ;

単一のセミコロン

`;;`は、インタプリタ環境などで式を区切るのに使ったが
単一のものは、式を式文に変換する。

具体的な変換は、`let () = expr in`に変換する

```ml
let expr1;
let expr2;
let expr3;
...
let exprN;

(* <=> *)

let () = expr1 in
let () = expr2 in
let () = expr3 in
...
exprN
```

最後に、`;`をつけなかった式が評価されて変える。
何も返したくないときは最後の式を `unit` にしたりする。

途中の式が`unit`を返さない場合は、警告されることもある。
`strict-sequence`フラグをコンパイラに渡すと警告される。


### let () = expr
エントリーポイントにする式と同じような書き方。
let 定義をするときは右辺を評価するため、
正格実行する式を書いて、戻り値は捨てるような場合に書くやり方。

右辺は副作用処理であることが多い。
(というかpureなコードを右辺に書いても意味がない)

### 副作用は最後に

副作用関数内では
pure コード -> impure コード
の順番で書いたほうが良い
(というか let ... in でバインドしたら自然とそうなりそう)

副作用処理で変更される前に純粋関数を実行したほうが、
データ構造が矛盾した場外になる可能性を抑えられる
良い習慣

# Primitive Mutable Data

最初からmutableなデータ
副作用的処理には必須

- Array
- Mutable Record

## Ordinary Array
通常の配列
固定長

`Array`モジュールに、リストのように操作する関数や
副作用的な変更を及ぼす関数が含まれる
- `Array.set` : 指定したインデックスに値をセット
- `Array.blit` : ある配列範囲をコピーする

また、インデックスでどの要素にもランダムアクセス(どの位置にも同じアクセス時間)できる
Listは単方向なので、Arrayと比べて後ろの要素へのアクセスが遅い

```ml
(* Read / Write *)
<array_expr>.(<index_expr>)
<array_expr>.(<index_expr>) <- <value_expr>

(* Literal *)
[| 1; 2; 3 |]
```

配列外にアクセスすると例外をスローされる


## string
文字列は基本的にバイト配列
プリミティブなデータ構造だが、内部的には `List`じゃなく`Array`らしい

### string vs Char.t array

stringのほうが、スペース効率がいいらしい
Char.tは1文字に付き1byteつかう


## BigArray.t

`BigArray.t`は、OCamlのメモリヒープの外側にあるメモリブロックへのハンドル。
CやFortranライブラリとのやり取りに役立つらしい。

Arrayと同じく、インデックスでランダムアクセスできる構文がある。


# Mutable Record
レコードを宣言するときに`mutable`をつけると、そのフィールドは`ref`になる。

# Ref Cells
mutableな変数を作成するときは、refを使うことになる

```ml
type 'a ref = {mutable contents : 'a}
```
実は、`Mutable Record Field`が先で、
それをラップする型とヘルパー関数、ヘルパー演算子があるだけ


```ml
type 'a ref = {mutable contents: 'a}

let ref x = {contents= x}
let ( ! ) ref = ref.contents
let ( := ) ref x' = ref.contents <- x'
```
- 型
- コンストラクタ
- deref
- 代入(セイウチ演算子)




# FFI
- 外部のプログラミング言語のライブラリ(C, Fortran)
- システムコール(shell)

これらにインターフェースをつけて呼び出すのも命令的操作

ユーザー定義のもの、内蔵されているもの
いろいろある

# for & while loop

再帰関数でこれらのループはシミュレートできるので
厳密には必要ない。

命令的プログラミングではこっち使ったほうが慣用的で簡潔かも。

## for

終わりの数字まで含まれる。
do done

```ml
for i = 0 to 3 do printf "%d" i done;;

for i = 3 downto 0 printf "%d" i done;;
```

ループ変数 i は、ループの中では不変

## while

最初に条件を評価する。
すなわち　do while ではない


```ml
let rev_inplace ar =
  let i = ref 0 in
  let j = ref (Array.length ar - 1) in
  (* terminate when the upper and lower indices meet *)
  while !i < !j do
    (* swap the two elements *)
    let tmp = ar.(!i) in
    ar.(!i) <- ar.(!j);
    ar.(!j) <- tmp;
    (* bump the indices *)
    Int.incr i;
    Int.decr j
  done
;;
```

whileの条件が更新されないと簡単に無限ループになるので注意

### Int.incr, Int.dcr

`int ref`型の中身をインクリメント/デクリメントするためだけの関数。
副作用処理で役立つかも


# 例. 双方向リンクリスト

`dlist.mli`

```ml
open Base

type 'a t
type 'a element

(** Basic list operations  *)
val create   : unit -> 'a t
val is_empty : 'a t -> bool

(** Navigation using [element]s *)
val first : 'a t -> 'a element option
val next  : 'a element -> 'a element option
val prev  : 'a element -> 'a element option
val value : 'a element -> 'a

(** Whole-data-structure iteration *)
val iter    : 'a t -> f:('a -> unit) -> unit
val find_el : 'a t -> f:('a -> bool) -> 'a element option

(** Mutation *)
val insert_first : 'a t -> 'a -> 'a element
val insert_after : 'a element -> 'a -> 'a element
val remove : 'a t -> 'a element -> unit
```

つくんのめんどい
型定義だけ見れば大体わかりそうなので、実装は飛ばすことにする。

```ml
module DList = struct
  type 'a element = {
    value: 'a;
    mutable next: 'a element option;
    mutable prev: 'a element option;
  }

  type 'a t = 'a element option ref
end
```

`DList.t`は、多分先頭要素へのアクセス?

`DList.element`が色々情報を持っている。

## implement

今更だけど、モジュール定義されたオブジェクトは
ヘルパー関数で引数にするときに`t`と略されることが多い気がする
`let func t: t = ...`って感じ

```ml
module DList = struct
  type 'a element =
    {value: 'a; mutable next: 'a element option; mutable prev: 'a element option}

  type 'a t = 'a element option ref

  let create () = ref None
  let is_empty t = Option.is_none !t
  let value elt = elt.value
  let first t = !t
  let next elt = elt.next
  let prev elt = elt.prev
end
```

ここまでのヘルパー関数は
基本的にレコードフィールドへのアクセスや
`deref`を関数でラップしてるだけ


## Cyclic Data Structure

```ml
let rec endless_loop = 1 :: 2 :: 3 :: endless_loop;;
val endless_loop : int list = [1; 2; 3; <cycle>]
```

サイクル構造のリストが定義されると、リスト要素が `<cycle>`になる。

限定的なケース


## リストの修飾

### insert_first
```ml
  let insert_first t value =
    let new_elt = {value; prev= None; next= !t} in
    ( match !t with
    | Some old_first -> old_first.prev <- Some new_elt
    | None -> () ) ;
    t := Some new_elt;
    new_elt
```

elt = element type

`match !t ...`の部分は、match式の優先順位が低くいため次の代入式と区別するために
`begin ... end`で囲む。
`()` でも同じ役割を果たせる。

挿入した要素を返す。


### insert_last

```ml
  let insert_after elt value =
    let new_elt = {value; prev= Some elt; next= elt.next} in
    (match elt.next with Some next -> next.prev <- Some new_elt | None -> ()) ;
    elt.next <- Some new_elt ;
    new_elt
```

ある要素の後ろに挿入する
- ある要素
- 値

1. ある要素をprev, ある要素のnextをnextにした新しい`new_elt`を作成
2. elt.nextがあったら、prevを`new_elt`に
3. ある要素のnextを`new_elt`に
4. new_elt を返す。

結構めんどい
でも要領は`insert_first`と同じ


### remove

リストの要素を削除
厳密には、前後の要素の next, prev をつなげて
自分自身の next, prev を None にする。

後者の操作は結構重要。
間違って参照を残しておいて、同じ要素を2回削除してしまうと簡単に壊れる。

```ml
  let remove t elt =
    let {prev; next; _} = elt in
    (match prev with Some prev -> prev.next <- next | None -> ()) ;
    (match next with Some next -> next.prev <- prev | None -> ()) ;
    elt.prev <- None ;
    elt.next <- None
```

### iter, find_el

- リスト全体に副作用関数を適用
- リストから要素をoptionでラップして見つける

```ml
  let iter t ~f =
    let rec loop = function None -> () | Some elt -> f elt ; loop elt.next in
    loop !t

  let find_el t ~f =
    let rec loop = function
      | None -> None
      | Some elt -> if f elt then Some elt else loop elt.next in
    loop !t
```

どちらも、
- `function`キーワードを使って引数パターンマッチした inner `rec loop` 関数を定義
- 先頭を表す `t` を deref `!t`して適用

という感じ

find_el の `~f:`引数は、テスト関数。
イテレーションの文脈で言うテスト関数 = `elt -> bool`の関数
`List.filter`にわたすやつと同じ


# Laziness & Other Benign Effects
benign = 良性の、良質な

関数型のpureなスタイルでプログラミングしたいが
コードパフォーマンスのため限定的に副作用を使いたい場合が多くある。

このような副作用は `benign effects` (良性効果)と呼ばれることがある。

pure functional programming のメリットを維持しつつ、命令的な処理をするのに有効

## Lazy.t
`benign effects`の一つ
必要になるまで計算されない値

OCaml は `lazy`キーワードが予約されていて、
簡単に遅延値を作れる

```ml
(* Lazy.t の作成 *)
let v = lazy (print_endline "performing lazy computation"; Float.sqrt 16.);;
(* - : float lazy_t = <lazy> *)
(* 評価 *)
Lazy.force v;;
Lazy.force v;;
```

一回目だけ副作用の処理が出る
2回目は値が確定してるので、途中の副作用は無視して正格評価された値だけ返す。

## lazy_t
`'a Lazy.t` は実際は `'a lazy_t`のエイリアス

```ml
type 'a t = 'a lazy_t [@@deriving_inline sexp]
```
S式で実装されてるっぽい。


# Lazy.t の独自実装
自分で実装して動作を確認してみる。

実は、この独自実装と `lazy`キーワードを使った遅延値の作成は内部処理に変わりがない
構文が違うだけ

- `create_lazy (fun () -> sqrt 16.)`
- `lazy (sqrt 16.)`

```ml
type 'a lazy_state =
  | Delayed of (unit -> 'a)
  | Value of 'a
  | Exn of exn
;;

let create_lazy f = ref (Delayed f)

let force v =
  match !v with
  | Value x -> x
  | Exn exn -> raise exn
  | Delayed f -> (
    try
      let x = f () in
      v := Value x ;
      x
    with exn ->
      v := Exn exn ;
      raise exn )

```
3つのVariant型で、遅延値の状態を示す

- Delayed : unit を適用すると値を返す関数を持っている
- Value : 値を持っている
- Exn : 例外が発生した状態(delayedの関数の中で発生した状態？)



関数
- create_razy
  - `lazy`値へのrefを作る
- force
  - lazy値を評価する
    - Value状態なら取り出して返す
    - Exn なら例外をraise
    - Delayedなら関数にunit適用して値を計算して返す

# thunk
e.g. = exempli gratia = for the sake of example = 例えば
i.e. = id, est = that is , つまり

thunk = ズシン、ドスン、ブスっという音
コンピューター用語的には 遅延評価を受ける、関数火引き渡す引数

遅延値は、`thunk` つまり `unitを引数に取る関数`でも作ることができる。
式の計算を途中で止める方法として有用

上の例でも、`Delayed f`のfは `unit -> 'a` で
式が thunk にラップされている状態


# Memoization and Dynamic Programming

もう一つの`benign effect`
それはメモ化 (Memoization) 

メモイゼーション (ライズじゃなくて)

メモ化された関数は、以前呼び出した関数の結果を記憶する。
よって、同じ引数を渡すとそれ以上の計算をせずに返すことができる。
(キャッシュとかと同じ原理)


## 関数のメモ化 trial
`Base.Hashtbl` (本格的な実装のやつ)を使って、
メモ化をやってみる

## memoize関数
関数を渡してメモ化して返す。

```ml
let memoize m f =
  let memo_table = Hashtbl.create m in
  fun x -> Hashtbl.find_or_add memo_table x ~default:(fun () -> f x)
```


`Hashtbl.find_or_add`
- keyがあったら返す
- なかったら、default関数に指定した `unit -> 'a` に unitを適用した結果を返す
  - ついでに、キーに結果を登録する

再計算にコストがかかる関数があり、
古い値を無期限キャッシュしても気にならないなら便利。

結果を保持し続けるとメモリリークするのでそれには注意

## メモ化 + Recursive Algorithms

いくつかの再帰的アルゴリズムを効率的に実装するのにも便利。

`Levenshtein distance`などがいい例
`the edit distance`を計算する。

一方の文字列を他方の文字列に変換するのに必要な1文字の変更の数
- 文字切り替え
- 挿入
- 削除

文字列マッチング問題にすごく役立つメモ化
(スペルチェッカーなど)


suffix = 接尾辞
ここでは文字列の最後の文字のこと

# the edit distance

```ml
let rec edit_distance s t =
  match (String.length s, String.length t) with
  | 0, x | x, 0 -> x
  | len_s, len_t ->
      let s' = String.drop_suffix s 1 in
      let t' = String.drop_suffix t 1 in
      let cost_to_drop_both =
        if Char.( = ) s.[len_s - 1] t.[len_t - 1] then 0 else 1 in
      List.reduce_exn ~f:Int.min
        [ edit_distance s' t + 1; edit_distance s t' + 1
        ; edit_distance s' t' + cost_to_drop_both ]
```

動作


1. s, tの二種類の文字を用意する
2. 末尾の文字が等しいか比較
  - 等しければ、両方ドロップしてもコスト0
  - 違う場合は両方ドロップしてコスト1
3. sだけ末尾1文字落としたs'、tだけ末尾一文字落としたt'を用意
4. 片方だけ落としてコスト +1 と 両方落として cost = 同じかどうか ? 0 : 1 のリストを用意
5. コストを足しながら再帰呼び出し
6. 最後に、`List.reduce`でリストの中から`Int.min`でコスト最小のものを返す
7. コスト最小のものがより合わさってできたものが最終的な最短編集コスト


動作イメージ
http://dev.realworldocaml.org/images/imperative-programming/edit_distance2.png


コレを実行すると、
再帰的な枝構造な感じになり、
いくつか同じ引数の呼び出しがかぶる場合がある。

それをメモ化しておくと、パフォーマンスが上がる


# 時間計測

```ml
let time f =
  let open Core in
  let start = Time.now () in
  let x = f () in
  let stop = Time.now () in
  printf "Time: %F ms\n" (Time.diff stop start |> Time.Span.to_ms);
  x
;;
```
`let ... in`で連続でローカルlet定義する間に
start計測 -> 関数実行　-> stop計測 して
差分を出力するだけ
(関数は thunk でラップしとく)


数文字足すだけで3千倍も遅くなった！
```ml
time (fun () -> edit_distance "OCaml" "ocaml");;
Time: 1.10292434692 ms
- : int = 2

time (fun () -> edit_distance "OCaml 4.09" "ocaml 4.09");;
Time: 3282.86218643 ms
- : int = 2
```

## Fibonacci sequence
一旦、物事の本質を見るために
簡単だけど指数関数的に計算量の増えるフィボナッチ数で考える

```ml
let rec fib = function 
| 0 -> 0 
| 1 -> 1 
| x -> fib (x - 1) + fib (x - 2)
```

```ml
time (fun () -> fib 20);;
Time: 1.12414360046 ms
- : int = 10946
time (fun () -> fib 40);;
Time: 18263.7000084 ms
- : int = 165580141
```
`fib 40`の時点で `fib 20` 10000 倍の計算量





## fib関数を早くする

そのまま、`momorize fib`しても無駄
fib関数の実装内部の関数適用でメモ化を入れないと駄目

計算部分を再帰関数でなくして、
再帰関数で計算部分を何度も呼ぶようにラップ
```ml
let fib_norec fib i = if i <= 1 then i else fib (i - 1) + fib (i - 2)
let rec fib i = fib_norec fib i
```


この、再帰的にする部分は `make_rec`関数にしても良い
```ml
let make_rec f_norec =
  let rec f x = f_norec f x in
  f

let fib = make rec fib_norec
```

### メモ化

ties recursive knot

再帰関数をメモ化するコツは、
- 再帰の結び目を見つけて、いったん再帰的ではない`f_norec`関数にする
- 再帰的でなくした関数を、`make rec`で再帰的にする
- その際に、結び目の関数呼び出しをメモ化する

つまり、`make_rec`の別バージョンで
メモ化までやる`make_memo`

```ml
let memo_rec m f_norec x =
  let fref = ref (fun _ -> assert false) in
  let f = memoize m (fun x -> f_norec !fref x) in
  fref := f ;
  f x
```

`fref`という、呼び出すと `assert false`になってプログラムが止まる例外がraiseされる関数へのrefがある。
ただし、この`fref`の中身は絶対呼び出されることがない。

`fref := f` によって、f の参照先は 自分自身になっているので
`ref (fun _ -> assert false)`への参照が変わってしまうため



計算してみる
```ml
let fib = memo_rec (module Int) fib_norec;;
time (fun () -> fib 40);;

Time: 0.130176544189 ms
- : int = 102334155
```
指数関数的な時間のかかり方はなくなり、
計算時間は約 10万分の1に
`18263.7` -> `0.13`


## (module Int)
memo_rec のハッシュテーブルを渡すべき部分に入ってるやつ
噂のファンクターと言うやつ？
(引数を渡すとモジュールが戻る?)

# edit_distance 関数を改良


edit_distance をメモ化する。

現在、edit_distance は文字列 s t の2引数関数なので、
(s, t)の2要素タプルの1引数関数にする。

更に、String_pairモジュールを作って
- S式(シリアライズ)
- ハッシュ関数
- 比較関数
を自動実装する


```ml
module String_pair = struct
  type t = string * string [@@deriving sexp_of, hash, compare]
end

(* 評価されるモジュールsig *)
module String_pair :
sig
  type t = string * string
  val sexp_of_t : t -> Sexp.t
  val hash_fold_t :
    Base_internalhash_types.state -> t -> Base_internalhash_types.state
  val hash : t -> int
  val compare : t -> t -> int
end
```


```ml
let edit_distance =
  memo_rec
    (module String_pair)
    (fun edit_distance (s, t) ->
      match (String.length s, String.length t) with
      | 0, x | x, 0 -> x
      | len_s, len_t ->
          let s' = String.drop_suffix s 1 in
          let t' = String.drop_suffix t 1 in
          let cost_to_drop_both =
            if Char.( = ) s.[String.length s - 1] t.[String.length t - 1] then 0
            else 1 in
          List.reduce_exn ~f:Int.min
            [ edit_distance (s', t) + 1; edit_distance (s, t') + 1
            ; edit_distance (s', t') + cost_to_drop_both ])
```


なんか中盤のラムダ式がよくわからんけど、
`edit_distance_norec`をlet定義するよりこっちのほうが良いということか



## let rec の制限

OCamlは`let rec f = ...` の右辺を厳格にチェックするので、
再帰定義した関数が呼び出しされないとエラー

```ml
let memo_rec m f_norec =
  let rec f = memoize m (fun x -> f_norec f x) in
  f
;;
```
`let rec f` が呼び出しされてなくて、関数に引数として渡されるだけなのでコンパイルできない

```ml
let rec x = x + 1
```

x を単項演算子と考えれば呼び出ししているといえなくもないが、
引数を取らない単なるInt値は無限ループに対応してないのでエラー
というか、関数ではなく普通の値なので`let rec`できない

```ml
let rec x = lazy (force x + 1);;
```

コレはできるらしい
でも、実際に計算しようとするとエラー



# lazy + memo_rec

lazyを使うと、mutableなしで
recursive memoization を実装できる。

```ml
let lazy_memo_rec m f_norec x =
  let rec f = lazy (memoize m (fun x -> f_norec (force f) x)) in
  (force f) x
```

`let rec` の右側はLazyなら大丈夫らしい



# Input & Output

命令的プログラミングでよくある I/O
メモリ内のデータ構造を変異させる以外に、
プログラムの外の世界との相互作用も命令的プログラミングに含まれる。

- ファイル
- ターミナル
- ネットワークソケット

これらからデータを読み書き


## Stdio
OCaml 標準ライブラリ

以下のモジュールがある
- In_channel
- Out_channel

`Core`ライブラリにも似たようなものがある

基本的に、In/Out_channelが直接サポートしているのは
- ファイル
- ターミナル


## Terminal I/O

- In_channel.stdin
- Out_channel.stdout
- Out_channel.stderr

実は、`Stdin`モジュールを介さずにグローバルネームスペースで使えるらしい
いわゆるグローバル変数


## In_channel モジュール

- input_line : 1行読み込み
  - チャンネルを選べる


## Out_channel モジュール

- flush : バッファをクリア
- output_string : 指定したチャンネルに文字列を出力
  - ファイル、stdout などを選べる

### なぜflushするか
flush とは = バッファリングされたものを、実際に出力に吐き出すこと

明示的に flush するか、
自動的に flush される量が書き込まれるまでフラッシュされない。

試しに Out_channel flush  stdout をコメントアウトすると
入力を促す出力が最後にでてくる

```ml
asia/tokyo
Pick a timezone: The time inasia/tokyo is 2020-06-23 13:36:28.411288+09:00.
```

明示的に flush を指定するのは、システムコールの回数を減らし書き込み処理の効率を向上させるためらしい
(大幅に上がる)

プログラムが終了する時は自動で flush されるので、
途中でflushしない場合は最後にドバッと出る


## Time モジュール
時間に関連することいろいろ

- now() : 現在時刻取得

## Time.Zone モジュール
タイムゾーンを文字列から探したりできる



```ml
dune exec ./main.exe
Pick a timezone: asia/tokyo
The time inasia/tokyo is 2020-06-23 13:29:13.641771+09:00.
```


# Formatted Output with printf

`Out_channel.printf`はC言語標準ライブラリのprintfをモデルにしている


```ml
printf
  "%i is an integer, %F is a float, \"%s\" is a string\n"
3 4.5 "five"
```
ただし、Cと異なりタイプセーフ
フォーマット文字列のプレースホルダーと型が合わないとタイプエラー
(嬉しい)


# Format String を理解する

OCamlのフォーマット文字列はタイプセーフ
すなわち、OCamlコンパイラはコンパイル時にフォーマット文字列の内容を解析する。

```ml
let fmt = "%i is an integer \n"
let () = printf fmt 3
```
これはコンパイルエラー
実はprintfの第一引数の型は `string`ではなく `('a, 'b, 'c) format`型らしい

```ml
let fmt : ('a, 'b, 'c) format = "%i is an integer \n"
let () = printf fmt 3
```
フォーマット文字列タイプであるという
型注釈を入れると通る


## ('a, 'b, 'c) format 型について
これは型システムの特殊なケースであり、
OCamlを普通に使う上で、この特殊な扱いについて熟達する必要はない


## format string によるチャンネルの制御
結構、いろんなことができる

- %! : チャンネルを flush する
- %s : 文字列の埋め込み

その他にも、

- alignment
- padding
- escape rule
- 2, 10, 16新数表示
- float変換の精度

## stdout 以外に出力する printf

- eprintf : stderr
- fprintf : 他の out_channel (ファイルとか)
- sprintf : 文字列の値として返す(string)

`Printf`モジュールのドキュメントを見るべし

# File I/O

`in_channel/out_channel`はターミナルだけでなく
ファイルから入力するのにもよく使う


```ml
open Core

let create_number_file filename numbers =
  let outc = Out_channel.create filename in
  List.iter numbers ~f:(fun x -> Out_channel.fprintf outc "%d\n" x) ;
  Out_channel.close outc

let sum_file filename =
  let file = In_channel.create filename in
  let numbers = List.map ~f:Int.of_string (In_channel.input_lines file) in
  let sum = List.fold ~init:0 ~f:( + ) numbers in
  In_channel.close file ; sum
```
`Out_channel.`
- `create` : ファイル名を渡して、そのファイルを作って `out_channel`型を返す
- `fprintf` : チャンネルを指定してフォーマット文字列を出力
- `close` : `out_channel`型を引数に取る。そのチャネルを閉じる


`Out_channel.t = out_channel`


In_channelも大体同じ
`create`関数だけでファイルが作れるのが簡単そう。


## Exn.protect : ファイルを開いている状態の例外に対処する

上の関数は、ファイルを最後で閉じるようにしている。
でも途中で例外が起こったらファイルを閉じないまま終了してしまう。
(閉じないとリソースをOSに戻すことができない)

`Exn.protect`関数を使う
`~finally: () -> ()` を設定しておけば
例外の発生問わずファイルを閉じられる

```ml

let create_number_file filename numbers =
  let outc = Out_channel.create filename in
  Exn.protect 
    ~finally:(fun () -> Out_channel.close outc)
    ~f:(fun () -> 
      List.iter numbers ~f:(fun x -> Out_channel.fprintf outc "%d\n" x)
    )
let sum_file filename =
  let file = In_channel.create filename in
  let numbers = List.map ~f:Int.of_string (In_channel.input_lines file) in
  let sum = List.fold ~init:0 ~f:( + ) numbers in
  Exn.protect ~finally:(fun () -> In_channel.close file) ~f:(fun () -> sum)

```
内部状態の変更など
副作用的な操作をするときは、
途中でエラーで停止してしまったことを考えながらプログラミングする必要がある

コレはすごく一般的な命令形プログラミングの問題解決パターン
finallyで閉じる。

# In_channl util
`In_channel`モジュールにはこのような細かいタスク
- 例外時、終了時に閉じる
- 一気に読み込まないで、1行ずつ読み込む

などを自動化してくれる関数がある

## In_channel.with_file

ファイルを開きつつ、そのファイルチャネルを引数とする関数式を引数にしてファイル操作する。

```ml
let sum_file filename =
  In_channel.with_file filename ~f:(fun file ->
      let numbers = List.map ~f:Int.of_string (In_channel.input_lines file) in
      List.fold ~init:0 ~f:( + ) numbers)
```
終わったら閉じる
例外で終了しても閉じる

(`Exn.protect`と似た感じ)



## In_channel.fold_lines

`In_channel.input_lines`は、チャネルを引数にして、すべての行を `string list`型で返す
言い換えると一括読み込み


こちらの方は、`List.fold`関数のように
accumulater を初期値として、1行ずつ何らかの統合処理をして
最終的に accumulator を返す


```ml
let sum_file filename =
  In_channel.with_file filename ~f:(fun file ->
      let numbers =
        In_channel.fold_lines file ~init:[] ~f:(fun acc line ->
            Int.of_string line :: acc) in
      List.fold ~init:0 ~f:( + ) numbers)
```

(改行が見づらい...)


### more

これらは`In_channel/Out_channel`一部の機能
I/Oに限らず、命令的プログラミングをやっていて
何らかの不便を感じたら自分で実装する前に
BaseやCoreに実装されてないかを確認してからやると
無駄手間が少ないかも
(自分が不便に思うことは他の人が解決してるかも)


# Order of Evaluation

strict language = 正格評価の言語

殆どのプログラミング言語(純粋関数型でない言語)では計算の順序は決まっている
また、変数にバインドしたり関数に渡す前に式の評価は終わる。
(正格評価)

例外的に、順番で条件分岐する時
前方の評価だけ必要だったら後方の評価が不要な場合に計算しないようにしたい

```ml
let x = Float.sin 120. in
let y = Float.sin 75.  in (* 負 *)
let z = Float.sin 128. in
List.exists ~f:(fun x -> Float.O.(x < 0.)) [x;y;z]
;;
- : bool = true
```
負の値になるsinを探す
yで止まるので、`z`の評価は不要

基本的に、必要になるまで計算しないという考えは `Lazy`を使えということ

```ml
let isok =
  let x = lazy (printf "1\n"; Float.sin 120.) in
  let y = lazy (printf "2\n"; Float.sin 75.) in
  let z = lazy (printf "3\n"; Float.sin 128.) in
  List.exists ~f:(fun x -> Float.O.(force x < 0.)) [x; y; z]
```

```ml
1
2
- : bool = true
```
2で止まってるので、正しく遅延評価できてる。


# OCamlのデフォルト評価戦略

Haskellはデフォルトで lazy (遅延)
OCamlはデフォルトで strict (正格)

OCamlがデフォルトでstrictを選択するのは
命令的プログラミングと lazy evaluation が上手く混ざらないため

遅延評価は、副作用がいつ起こるか推論するのが難しい
副作用の発生タイミングは命令的プログラミングの振る舞いに直結する。

ちなみに、式内の評価順序はきまってない。
デフォルトから変えられないので、自分の好まない挙動になる場合もある。

```ml
List.exists ~f:(fun x -> Float.O.(x < 0.))
  [ (printf "1\n"; Float.sin 120.);
    (printf "2\n"; Float.sin 75.);
    (printf "3\n"; Float.sin 128.); ]
;;
3
2
1
- : bool = true
```

# 副作用と弱い多態性

このコードはポリモーフィックが聞かない

```ml
let remember =
  let cache = ref None in
  (fun x ->
     match !cache with
     | Some y -> y
     | None -> cache := Some x; x)
```

引数をそのまま返す。
呼び出したときに戻り値をキャッシュする。(次回以降はキャッシュを返す)

型はこんな感じ
```ml
val remember : '_weak1 -> '_weak1 = <fun>
```

# '_weak1
コレは型変数
弱い多相性は、どのような具体的な方として使われるかの手がかりを得ると
すぐに具体的な型に変換される

```ml
(* 最初に '_weak1 = char として使われたので *)
(* char -> char *)
let remember =
  let cache = ref None in
  fun x ->
    match !cache with
    | Some y -> y
    | None ->
        cache := Some x ;
        x

let a = remember 'a' (* ok *)
let b = remember 1 (* NG *)
```

# The Value Restriction

mutable ref cell の使われ方によって多相性が異なる。

- 2回目以降の呼び出しで継続する参照を使う関数は弱い多相性(1回決まったら変えられない)
  - 同じ参照を使い回す、内部状態を持つ
- 毎回新鮮な参照を用意する関数は完全な多相性(他の純粋関数と変わらない多相性)


# 部分適用と値制限

```ml
List.init 10 ~f:(fun i -> i)
```
リストを決めた要素分だけ初期化する関数。


部分適用すると、`'_weak`という弱い多相性変数がでてくる
```ml
List.init 10
f:(int -> '_weak3) -> '_weak3 list = <fun>
```

`~f`部分を関数の引数として用意して、
部分適用せずに完全適用することで、弱い多態性を回避できる

```ml
let list_init_10 ~f = List.init 10 ~f;;
val list_init_10 : f:(int -> 'a) -> 'a list = <fun>
```


# 値制限の緩和

戻り値が immutableなとき、完全な多態性を維持できる
```ml
identity [];;
- : 'a list = []
```

mutableだと、弱い多態性
```ml
[||];;
- : 'a array = [||]
identity [||];;
- : '_weak5 array = [||]
```

## mli で公開するときの欠陥
データ構造をモジュールとして公開する時、
`.mli`を通じて公開するが、そのときに型変数で抽象化されると
immutableな型であるにも関わらず、mutableに間違われることがある

```ml
Concat_list.empty;;
- : 'a Concat_list.t = <abstr>
identity Concat_list.empty;;
- : '_weak6 Concat_list.t = <abstr>
```

`Concat_list.empty`の戻り値は`[]`なのでimmutable
上の例で言う、`戻り値がimmutableな時、完全な多態性を維持できる`というルールが聞いていない。


## +'a

シグネチャの型パラメータに`+`をつけると、
データ構造が`'a`への永続的な参照を含まないことを明示できる

```ml
module Concat_list : sig
  type +'a t
```

型推論で、弱い多相性を使わず完全な多相性を維持できる。


# まとめ

- mutable data structure
  - mutable record field
  - mutable ref
    - `{mutable content: 'a}`

- 命令的プログラミング
  - for
  - while
    - Int.incr , Int.decr
- 古典的な命令形データ構造
  - HashTable
- 良い副作用 (benign effects)
  - Memoize (メモ化)
  - Lazy (遅延評価)
- Blocking I/O
  - In_channel / Out_channel
    - with_file
      - Exn.protect の便利バージョン
    - fold_lines

- 言語レベルの評価順の話
  - 正格評価 vs 遅延評価
    - OCamlのデフォルトは正格評価
    - 遅延評価は命令形プログラミングと相性が悪い
      - 副作用の発生タイミングが分かりづらい
      - 命令形プログラミングの振る舞いに直結するので、振る舞いを予測しづらい
  - 弱い多相性
    - 関数の中で、永続的な参照を使うと多相性が弱くなる
      - 一回、型の手がかりが決定したら変更できなくなる
    - 戻り値が immutable なときだけ完全な多相性を維持できる
      - `+'a`
      - データ構造が `'a` 型パラメータへの永続的な参照を持ってないことを示す


